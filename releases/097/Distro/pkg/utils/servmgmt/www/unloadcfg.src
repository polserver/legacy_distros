/* $Id$
 *
 */

use uo;
use http;
use cfgfile;

include ":servmgmt:header";

Program HTMLPage()
	DoHeader("Server Management Config Unloader");

	TableHeader("Config Unloader");

	var fname := QueryParam("file");
	if ( Len(fname) > 1 || QueryParam("Unload") )
		if ( fname == "*ALL*" )
			fname := "";
		else
			while ( fname["#"] )
				fname["#"] := ":";
			endwhile
			while ( fname["%"] )
				fname["%"] := "/";
			endwhile
		endif
		var res := UnloadConfigFile(fname);

		WriteHTML("<BR><BR>");

		WriteHTML("<TABLE WIDTH='400'>");
		WriteHTML("<TR><TD CLASS='header'>Unload Result</TD></TR>");
		if ( res == error )
			WriteHTML("<TR><TD>Error unloading config file: "+res.errortext+"</TD></TR>");
		elseif ( res < 0 )
			WriteHTML("<TR><TD>That config file is not loaded.</TD></TR>");
		else
			WriteHTML("<TR><TD>Config file unloaded. "+res+" reference(s) to config file exist.</TD></TR>");
		endif
		WriteHTML("</TABLE>");
	endif

	ShowForms();

	return 1;
endprogram

function ShowForms()
	WriteHTML("<BR><BR>");

	WriteHTML("<TABLE WIDTH='400'>");
	WriteHTML("<FORM NAME='FRM' METHOD='GET'>");
	WriteHTML("<TR><TD CLASS='header2' COLSPAN='2'>Which config file do you want to unload?</TD></TR>");
	WriteHTML("<TR><TD COLSPAN='2'>Use '#'s instead of ':'s and ommit the '.cfg'</TD></TR>");
	WriteHTMLRaw("<TR>");
	WriteHTMLRaw("<TD><INPUT TYPE='TEXT' NAME='file' SIZE='40'></TD>");
	WriteHTMLRaw("<TD><INPUT TYPE='SUBMIT' NAME='Unload' VALUE='Unload'></TD>");
	WriteHTML("</TR>");
	WriteHTML("</FORM>");
	WriteHTML("</TABLE>");

	return 1;
endfunction