/* $Id$
 *
 */

use uo;
use os;
use http;
use polsys;

include ":servperf:header";
include ":attributes:attributes";
include ":containers:storageAreas";
include "include/uo_extend";

unload_scripts("");

program HTMLPage()
	DoHeader("Object Viewer");
	TableHeader("Object Viewer");

	var list := QueryParam("List");
	//var serial := QueryParam("Serial");
	var acct_name := QueryParam("Account");
	if ( acct_name )
		ShowAccountInfo(acct_name);
	elseif ( list )
		case ( list )
			"Accounts":
				AcctLister();
				break;
			"Online":
				OnlineLister();
				break;
			"Storage":
				StorageLister();
				break;
			default:
				DoError("Unknown 'List' type!");
				break;
		endcase
	else
		StartPoint();
	endif

	DoFooter();
	return 1;
endprogram

function StartPoint()
	WriteHTML("<BR>");

	WriteHTML("<TABLE WIDTH=400>");
	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header2'>Select a start point.</TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>");
	WriteHTML("<LI><A HREF='?List=Accounts'>Accounts</A></LI>");
	WriteHTML("<LI><A HREF='?List=Online'>Online Characters</A></LI>");
	WriteHTML("<LI><A HREF='?List=Storage'>Storage Areas</A></LI>");
	WriteHTML("<BR>");
	WriteHTML("</TD>");
	WriteHTML("</TR>");
	WriteHTML("</TABLE>");

	WriteHTML("<BR>");

	WriteHTML("<FORM METHOD='GET'>");
	WriteHTML("<TABLE WIDTH=400>");
	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header2' COLSPAN='3'>Search</TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>Search Text</TD><TD COLSPAN='2'><INPUT TYPE='TEXT' SIZE='45' NAME='SearchText'></TD>");
	WriteHTML("</TR>");

	WriteHTML("<TR>");
	WriteHTML("<TD ROWSPAN='4'>Search Where</TD>");
	WriteHTML("<TD>Accounts</TD><TD ALIGN='CENTER'><INPUT TYPE='RADIO' NAME='SearchWhere' VALUE='Accounts'></TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>Top Level Items</TD><TD ALIGN='CENTER'><INPUT TYPE='RADIO' NAME='SearchWhere' VALUE='TLIs'></TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>Storage Areas</TD><TD ALIGN='CENTER'><INPUT TYPE='RADIO' NAME='SearchWhere' VALUE='Storage'></TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>Players</TD><TD ALIGN='CENTER'><INPUT TYPE='RADIO' NAME='SearchWhere' VALUE='Players'></TD>");
	WriteHTML("</TR>");

	WriteHTML("<TR>");
	WriteHTML("<TD ROWSPAN='2'>Search What</TD>");
	WriteHTML("<TD>Name/Desc</TD><TD ALIGN='CENTER'><INPUT TYPE='RADIO' NAME='SearchWhat' VALUE='NameDesc'></TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>Serial</TD><TD ALIGN='CENTER'><INPUT TYPE='RADIO' NAME='SearchWhat' VALUE='Serial'></TD>");
	WriteHTML("</TR>");

	WriteHTML("<TR>");
	WriteHTML("<TD COLSPAN='3' ALIGN='CENTER'><INPUT TYPE='SUBMIT' NAME='Submit' VALUE='Submit'></TD>");
	WriteHTML("</TR>");

	WriteHTML("</FORM>");
	WriteHTML("</TABLE>");

	return 1;
endfunction

function DoError(text)
	WriteHTML("<TABLE>");
	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header'>!! ERROR !!</TD>");
	WriteHTML("</TR>");
	WriteHTML("<TD><FONT COLOR='RED'>"+text+"</FONT></TD>");
	WriteHTML("</TR>");
	WriteHTML("</TABLE>");

	return 1;
endfunction

function LocationBar(tree:=0)
	WriteHTML("<TABLE WIDTH='100%'>");
	WriteHTML("<TR>");
	WriteHTMLRaw("<TD CLASS='tree'>");
	WriteHTMLRaw("<A HREF='?'>Index</A>");

	var tree_size := tree.Size();
	var i := 1;
	for ( i:=1; i<=tree_size; i+=2 )
		WriteHTMLRaw("<B> -> </B>");
		WriteHTMLRaw(" <A HREF='?"+tree[i]+"'>"+tree[i+1]+"</A>");
		SleepMS(2);
	endfor
	WriteHTML("</TD>");
	WriteHTML("</TR>");
	WriteHTML("</TABLE>");
	WriteHTML("<BR>");

	return 1;
endfunction

function FilterBar(selected)
	WriteHTML("<TABLE WIDTH='100%'>");
	WriteHTML("<TR>");
	WriteHTMLRaw("<TD>");
	var link := "<A HREF='?List=Accounts&Ext=%23'>#</A> ";
	if ( selected == "#" )
		link := "<B><I>"+link+"</I></B>";
	endif
	WriteHTMLRaw(link);
	var i;
	for ( i:=65; i<=90; i+=1 )
		var letter := CChr(i);
		link := "<A HREF='?List=Accounts&Ext="+letter+"'>"+letter+"</A>";
		if ( letter == selected )
			link := "<B><I>"+link+"</I></B>";
		endif
		WriteHTMLRaw(link+" ");
		SleepMS(2);
	endfor
	link := "<A HREF='?List=Accounts&Ext=All'>All</A> ";
	if ( selected == "All" )
		link := "<B><I>"+link+"</I></B>";
	endif
	WriteHTMLRaw(link);
	WriteHTML("</TD>");
	WriteHTML("</TR>");
	WriteHTML("</TABLE>");

	WriteHTML("<BR>");

	return 1;
endfunction

function SortList(byref string_array, letter:="")
	letter := Upper(letter);
	var dict := dictionary;

	foreach entry in ( string_array )
		entry[1] := Upper(entry[1]);
		var first_letter := entry[1];
		if ( first_letter == letter )
			if ( !dict.Exists(first_letter) )
				dict[first_letter] := array;
			endif
			dict[first_letter].Append(entry);
		elseif ( letter == "#" )
			first_letter := CAsc(entry[1]);
			if ( first_letter >= CAsc("0") && first_letter <= CAsc("9") )
				if ( !dict.Exists(first_letter) )
					dict["#"] := array;
				endif
				dict["#"].Append(entry);
			endif
		elseif ( letter == "ALL" )
			if ( CAsc(first_letter) >= CAsc("0") && CAsc(first_letter) <= CAsc("9") )
				first_letter := "#";
			endif

			if ( !dict.Exists(first_letter) )
				dict[first_letter] := array;
			endif

			dict[first_letter].Append(entry);
		endif

		SleepMS(2);
	endforeach

	return dict;
endfunction

function AcctLister()
	LocationBar();

	var selected := QueryParam("Ext");
	if ( !selected )
		selected := "All";
	endif
	FilterBar(selected);
	var acct_list := SortList(ListAccounts(), selected);

	WriteHTML("<TABLE WIDTH=400>");
	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header2'>");
	WriteHTML("Total Accounts: "+acct_list.Size());
	WriteHTML("</TD>");
	WriteHTML("</TR>");
	foreach list in ( acct_list )
		WriteHTML("<TR>");
		WriteHTML("<TD>");
		WriteHTML("<B>"+_list_iter+"</B><BR>");

		foreach account in ( list )
			WriteHTML("<LI><A HREF='?Account="+account+"'>"+account+"</A></LI>");
			SleepMS(2);
		endforeach
		WriteHTML("</TD>");
		WriteHTML("</TR>");
		sleepms(2);
	endforeach
	WriteHTML("</TABLE>");

	return 1;
endfunction

function ShowAccountInfo(acct_name)
	LocationBar(array{"List=Accounts", "Accounts"});

	var account := FindAccount(acct_name);
	if ( !account )
		DoError("Could not get account "+acct_name+"<BR> "+account.errortext);
		return 0;
	endif

	AccountCharacters(account);
	ShowCPropInfo(account);

	return 1;
endfunction

function AccountCharacters(account, highlight:=0)
	WriteHTML("<TABLE WIDTH='400'>");
	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header'>Characters on account "+account.name+"</TD>");
	WriteHTML("</TR>");

	for i:=1 to 6
		var character := account.GetCharacter(i);
		WriteHTML("<TR>");
		WriteHTML("<TD>");
		if ( character )
			var line := "<A HREF='?Serial="+character.serial+"'>"+character.name+"</A>";
			if ( highlight == character )
				line := "<B><I>"+line+"</I></B>";
			endif
			WriteHTML(line);
		else
			WriteHTML("Empty Slot");
		endif
		WriteHTML("</TD>");
		WriteHTML("</TR>");
		SleepMS(2);
	endfor
	WriteHTML("</TABLE>");

	WriteHTML("<BR><BR>");

	return 1;
endfunction

function OnlineLister()
	LocationBar();

	var online := EnumerateOnlineCharactersABC();
	WriteHTML("<TABLE WIDTH=400>");
	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header2'>");
	WriteHTML("Total Online: "+online.Size());
	WriteHTML("</TD>");
	WriteHTML("</TR>");
	WriteHTML("<TR>");
	WriteHTML("<TD>");
	foreach player in ( online )
		WriteHTML("<LI>"+player.name+"</LI>");
		SleepMS(2);
	endforeach
	WriteHTML("</TD>");
	WriteHTML("</TR>");
	WriteHTML("</TABLE>");

	return 1;
endfunction

function StorageLister()
	LocationBar();

	return 1;
endfunction

function ShowCpropInfo(object)
	WriteHTML("<TABLE WIDTH='400'>");
	WriteHTML("<TR>");
	WriteHTML("<TD COLSPAN='2' CLASS='header2'>");
	if ( object.name )
		WriteHTML("<B>CProps on "+object.name+"</B>");
	else
		WriteHTML("<B>CProps on "+object.desc+"</B>");
	endif
	WriteHTML("</TD>");
	WriteHTML("</TR>");

	WriteHTML("<TR>");
	WriteHTML("<TD CLASS='header2'>Prop Name</TD>");
	WriteHTML("<TD CLASS='header2'>Prop Value</TD>");
	WriteHTML("</TR>");

	foreach prop_name in ( object.PropNames() )
		WriteHTML("<TR>");
		WriteHTMLRaw("<TD>"+prop_name+"</TD>");
		WriteHTML("<TD>"+object.GetProp(prop_name)+"</TD>");
		WriteHTML("</TR>");
		SleepMS(2);
	endforeach
	WriteHTML("</TABLE>");

	WriteHTML("<BR>");

	return 1;
endfunction

