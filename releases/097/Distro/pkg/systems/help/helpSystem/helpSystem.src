/* $Id$
 *
 */

use uo;
use os;

include ":datafile:datafile";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":gumps:gumpprompt";
include ":help:help";
include ":help:help-gumps";

unload_scripts("");

program PageRequest(mobile)
	if ( GetProcess(GetObjProperty(mobile, "#HelpPID")) )
		SendSysMessage(mobile, "You already have a help gump open.");
		return 0;
	else
		SetObjProperty(mobile, "#HelpPID", GetPid());
	endif
	var settings := HS_GetSettingsCfgElem("Settings");

	var input, gump;
	var cur_menu := MAIN_PAGE_BTN;
	var cur_txt := array;

	while ( mobile.connected )
		if ( cur_menu == HELP_TOPIC_START )
			gump := RetrieveTopicGump(input[0], settings);
		else
			gump := RetrieveGump(cur_menu, settings, cur_txt);
		endif

		input := GFSendGump(mobile, gump);
		SendSysMessage(mobile, "Input->"+input[0]);
		if ( !input[0] || input[CLOSE_BTN] )
			SendSysMessage(mobile, "Cancelled.");
			break;
		elseif ( input[SEND_PAGE_BTN] )
			if ( cur_menu == COUNSELOR_PAGE_BTN )
				GumpPrompt(mobile, "Counselor paging not yet implemented.");
			elseif ( cur_menu == STAFF_PAGE_BTN )
				GumpPrompt(mobile, "Staff paging not yet implemented.");
			endif
		elseif ( input[WORD_WRAP_BTN] )
			cur_txt := GFWordWrap(RetrievePageText(input), 370);
		elseif ( input[MAIN_PAGE_BTN] )
			cur_menu := MAIN_PAGE_BTN;
		elseif ( input[COUNSELOR_PAGE_BTN] )
			cur_menu := COUNSELOR_PAGE_BTN;
		elseif ( input[STAFF_PAGE_BTN] )
			cur_menu := STAFF_PAGE_BTN;
		elseif ( input[HELP_TOPICS_BTN] )
			cur_menu := HELP_TOPICS_BTN;
		elseif ( input[0] >= HELP_TOPIC_START )
			cur_menu := HELP_TOPIC_START;
		else
			SendSysMessage(mobile, "Cancelled.");
			break;
		endif
	endwhile

	return 1;
endprogram

function RetrieveGump(cur_menu, byref settings, byref cur_txt)
	var gump;
	case ( cur_menu )
		MAIN_PAGE_BTN:
			gump := 0;// := LoadHelpInfo("MainGump", "Gump");
			if ( !gump )
				gump := BuildIntroGump(settings);
				SaveHelpInfo("MainGump", "Gump", gump);
			endif
			break;

		COUNSELOR_PAGE_BTN:
			gump := 0;// := LoadHelpInfo("CounselorPage", "Gump");
			if ( !gump )
				gump := BuildTextEntryGump("Counselor Page", settings, cur_txt);
				SaveHelpInfo("CounselorPage", "Gump", gump);
			endif
			break;

		STAFF_PAGE_BTN:
			gump := 0;// := LoadHelpInfo("StaffPage", "Gump");
			if ( !gump )
				gump := BuildTextEntryGump("Staff Page", settings, cur_txt);
				SaveHelpInfo("StaffPage", "Gump", gump);
			endif
			break;

		HELP_TOPICS_BTN:
			gump := 0;// := LoadHelpInfo("HelpTopicIndex", "Gump");
			if ( !gump )
				gump := BuildHelpTopicsIndexGump(settings);
				SaveHelpInfo("HelpTopicsIndex", "Gump", gump);
			endif
			break;

		default:
			gump := BuildGumpTemplate(settings);
			GFTextLine(gump, 10, 25, 32, "!!! --- INVALID CUR_MENU ["+cur_menu+"] --- !!!");
			break;
	endcase

	return gump;
endfunction

function RetrieveTopicGump(byref input, byref settings)
	input -= HELP_TOPIC_START;
	var topic_list := LoadHelpInfo("TopicNames", "List");
	var topic_name := topic_list[input];

	var gump := LoadHelpInfo("HelpTopic-"+topic_name, "Gump");
	if ( !gump )
		gump := BuildHelpTopicGump(topic_name, settings);
		SaveHelpInfo("HelpTopic-"+topic_name, "Gump", gump);
	endif

	return gump;
endfunction

function RetrievePageText(byref input)
	var i;
	var text := "";
	for ( i:=PAGE_TXT_START; i<=PAGE_TXT_END; i+=1 )
		var data := GFExtractData(input, i);
		if ( data )
			text += data+" ";
		endif
		SleepMS(2);
	endfor

	return text;
endfunction
