/*
 * $Id$
 *
 */

use uo;
use os;

include ":attributes:attributes";
include ":magery:spells";
include ":magery:spellSounds";
include "include/client";
include "include/sounds";

program SpellScript_Heal(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;
	
	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	// Amount healed: ((Magery / 10) + (1 to 3))
	var points := (CInt(AP_GetSkill(mobile, MAGERY)) / 10) + RandomDiceRoll("1d3");
	
	PlayObjectCenteredEffect(targ, GFX_BLUE_SPARKLE_SWIRL, 7, 16);
	PlaySoundEffect(targ, SFX_SPELL_HEAL);

	var healed := AP_GetVital(targ, "Hits");
	HealDamage(targ, points);
	healed := AP_GetVital(targ, "Hits") - healed;

	SendSysMessage(mobile, "You healed "+healed+" hit points of damage.");
	
	if ( mobile.serial != targ.serial )
		SendSysMessage(targ, "You have had "+healed+" hit points of damage healed.");
	endif
	
	return 1;
endprogram
