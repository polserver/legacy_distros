/*
 * $Id$
 *
 */

use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";

program SpellScript_Heal(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;
	
	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS + TGTOPT_HELPFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	// Amount healed: ((Magery / 10) + (1 to 3))
	var points := (CInt(AP_GetSkill(mobile, MAGERY)) / 10) + RandomDiceRoll("1d3");
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	var healed := AP_GetVital(targ, HITS);
	HealDamage(targ, points);
	healed := AP_GetVital(targ, HITS) - healed;

	SendSysMessage(mobile, "You healed "+healed+" hit points of damage.");
	
	if ( mobile.serial != targ.serial )
		SendSysMessage(targ, "You have had "+healed+" hit points of damage healed.");
	endif
	
	return 1;
endprogram
