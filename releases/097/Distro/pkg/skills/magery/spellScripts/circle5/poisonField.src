use uo;
use os;

include "include/facings";
include ":magery:spells";
include ":magery:spellSounds";
include ":magery:fields";
include ":attributes:attributes";

program SpellScript_PoisonField(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;
	
	var targ := MS_TargetCoordinates(mobile, info.targ, "Select a target.");

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif
	
	PlaySoundEffect(targ, SFX_SPELL_POISON_FIELD);
	
	// Duration: (3 + (Magery * 0.4)) seconds
	var duration := CInt(3 + AP_GetSkill(mobile, MAGERY) * 0.4);
	
	// Level is the same as Poison spell
	var level := MS_GetPoisonLevel(mobile, targ);
	
	if ( targ.facing == 1 || targ.facing == 2 || targ.facing == 5 || targ.facing == 6 )
		CreateField(targ, FIELD_SHAPE_HORIZONTAL, "PoisonField_NS", level, duration, "1d2", mobile, "1");
	else
		CreateField(targ, FIELD_SHAPE_VERTICAL, "PoisonField_EW", level, duration, "1d2", mobile, "1");
	endif
	
	return 1;
endprogram
