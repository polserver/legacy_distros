use uo;
use os;

include ":attributes:attributes";
include ":magery:spellSounds";
include ":magery:spells";
include "include/client";
include "include/sounds";
include ":damage:damage";

program SpellScript_MindBlast(params)
	var mobile := params[1];
	var info := params[2];
	var spell_id := params[3];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	PlaySoundEffect(mobile, SFX_SPELL_MIND_BLAST);
	PlayObjectCenteredEffect(mobile, GFX_RED_SPARKLES, 7, 16);
	
	// damage: Base Damage + ((Magery + Intelligence) / 5) (Drop decimals from result)
	var random_damage := CDbl(RandomInt(CInt((info.max_damage - info.min_damage) * 10))) / 10;
	var base_damage := info.min_damage + random_damage;
	var damage := base_damage + CInt((AP_GetSkill(mobile, "Magery") + AP_GetStat(mobile, "Intelligence")) / 5);

	SendSysMessage(mobile, "Mindblast for: "+damage);
	ApplyDamageEX(targ, damage, info.damage_type, mobile);
	
	return 1;
endprogram
