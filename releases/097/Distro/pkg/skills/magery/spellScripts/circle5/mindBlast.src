use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spellSounds";
include ":magery:spells";
include ":attributes:attributes";
include ":damage:damage";

program SpellScript_MindBlast(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	PlaySoundEffect(targ, SFX_SPELL_MIND_BLAST);
	PlayObjectCenteredEffect(targ, GFX_RED_SPARKLES, 7, 16);
	
	// Base damage is a decimal number in between min and max damage
	var min_damage := CInt(GetConfigInt(info.spell_config, "MinBaseDamage"));
	var max_damage := CInt(GetConfigInt(info.spell_config, "MaxBaseDamage"));
	var base_damage := min_damage + (CDbl(RandomInt(CInt((max_damage - min_damage) * 10))) / 10);

	// damage: Base Damage + ((Magery + Intelligence) / 5) (Drop decimals from result)
	var damage := base_damage + CInt((AP_GetSkill(mobile, MAGERY) + AP_GetStat(mobile, INTELLIGENCE)) / 5);

	ApplyDamageEX(targ, damage, info.damage_type, mobile);
	
	return 1;
endprogram
