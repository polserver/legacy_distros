use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";

program SpellScript_Cure(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS + TGTOPT_HELPFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( !targ.IsA(POLCLASS_ITEM) )
		// Doesn't do anything to mobiles
		SendSysMessage(mobile, "This spell wont work on that!");
		return 0;
	endif

	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	var area_size := GetConfigInt(info.spell_config, "AreaSize");
	var items := ListItemsNearLocation(targ.x, targ.y, targ.z, area_size, targ.realm);
	
	foreach item in items
		if ( GetObjProperty(item, "SummonedField") == 1 )
			DestroyItem(item);
		endif
	endforeach
	
	return 1;
endprogram
