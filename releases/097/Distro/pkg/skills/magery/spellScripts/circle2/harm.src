use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spellSounds";
include ":magery:spells";
include ":attributes:attributes";
include ":damage:damage";

program SpellScript_Harm(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS + TGTOPT_HARMFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	PlaySoundEffect(targ, SFX_SPELL_HARM);
	PlayObjectCenteredEffect(targ, GFX_RED_SPARKLES, 7, 16);
	
	var base_damage := info.min_damage + (CDbl(RandomInt(CInt((info.max_damage - info.min_damage) * 10))) / 10);
	var damage := MS_GetSpellDamage(mobile, targ, base_damage);

	// 1 tile = full damage, 2 tiles = half damage, >3 tiles = 1/4 damage
	case ( Distance(mobile, targ) )
		1: break;
		2: damage := CDbl(damage) / 2;
		default: CDbl(damage) / 4;
	endcase

	ApplyDamageEX(targ, damage, info.damage_type, mobile);
	
	return 1;
endprogram
