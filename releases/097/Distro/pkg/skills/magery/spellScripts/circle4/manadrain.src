/*
 * $Id$
 *
 */

use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";
include ":damage:damage";

program SpellScript_ManaDrain(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS + TGTOPT_HARMFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	if ( mobile.serial == targ.serial )
		SendSysMessage(mobile, "You cannot mana drain yourself!");
		return 0;
	endif
	
	PlaySoundEffect(targ, SFX_SPELL_MANA_DRAIN);
	PlayObjectCenteredEffect(targ, GFX_RED_SPARKLES, 7, 7);

	var mana_drained := 40 + AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) - AP_GetSkill(targ, RESISTING_SPELLS);
	var targ_mana := AP_GetVital(targ, MANA);

	if ( mana_drained > targ_mana )
		mana_drained := targ_mana;
	endif
	
	// Tell spell starter to not wait for this script to finish
	var spell_starter := GetProcess(info.pid);
	spell_starter.SendEvent("continue");
	 
	// Drain mana from targ and restore lost points after 4 seconds
	AP_ConsumeVital(targ, MANA, mana_drained);
	AP_SetVital(mobile, MANA, AP_GetVital(mobile, MANA) + mana_drained);
	Sleep(4);
	AP_SetVital(targ, MANA, AP_GetVital(targ, MANA) + mana_drained);

	return 1;
endprogram
