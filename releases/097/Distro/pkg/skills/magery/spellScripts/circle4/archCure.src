use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";
include ":timedScripts:poisonUtil";

program SpellScript_ArchCure(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	MS_PlaySpellSFX(info.spell_id, mobile);

	// TODO: Get non-hostile blues instead of people just in party
	// Get all targets in area that are in party.
	var area_size := CInt(GetConfigInt(info.spell_config, "AreaSize"));
	var includes := GetObjProperty(mobile, "#Party");
	var targs := MS_GetVaildTargets(mobile, area_size, 1, "", includes);

	foreach vtarg in targs
		// Get poison level
		// Note: Unlike OSI, we can have multiple poisons so we'll just grab the highest one.
		var poison_names := TS_GetTimerNames(vtarg, "P");
		var poison_level := 0;
		foreach timer_name in ( poison_names )
			var timer_info := TS_GetTimerInfo(mobile, timer_name);
			if ( timer_info.level > poison_level )
					poison_level := timer_info.level;
			endif
		endforeach
		
		//% Chance to cure: (Magery * 0,75) + (110 - (Poison level * 33)) .
		var chance := CInt(AP_GetSkill(mobile, MAGERY) * 0.75) + (110 - (poison_level * 33));
		if ( chance >= RandomInt(100) + 1 )
			// Should be enough to cure all poison.
			TS_CurePoison(vtarg, 10000, "P", 10);
		endif

		MS_PlaySpellGFX(info.spell_id, vtarg, mobile);
	endforeach

	return 1;
endprogram
