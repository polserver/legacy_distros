use uo;
use os;

include ":magery:spells";
include ":death:resurrection";

program SpellScript_Resurrection(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( mobile.serial == targ.serial )
		SendSysMessage(mobile, "Thou cannot resurrect thyself.");
		return 0;
	elseif ( !targ.dead )
		SendSysMessage(mobile, "Target is not dead.");
		return 0;
	elseif ( mobile.dead )
		// It's in the cliloc but I'm not sure how it could happen.
		SendSysMessage(mobile, "The resurrecter must be alive.");
		return 0;
	elseif ( Distance(mobile, targ) > 1 )
		SendSysMessage(mobile, "Target is not close enough.");
		return 0;
	elseif ( targ.hidden )
		SendSysMessage(mobile, "Target cannot be seen.");
		return 0;
	elseif ( !targ.IsA(POLCLASS_MOBILE) || targ.IsA(POLCLASS_NPC) )
		SendSysMessage(mobile, "Target is not a being.");
		return 0;
	endif

	var settings_elem := MS_GetSettingsCfgElem("Settings");
	
	if ( CInt(settings_elem.AskToRes) )
		if ( !ResurrectionGump(targ) )
			return 0;
		endif
	endif
	
	if ( Resurrection(targ) )
		if ( targ.criminal )
			mobile.SetCriminal(1);
		endif
	endif

	return 1;
endprogram
