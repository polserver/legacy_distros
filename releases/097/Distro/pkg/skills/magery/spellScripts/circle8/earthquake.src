use uo;
use os;

include ":attributes:attributes";
include ":magery:spells";
include ":magery:spellSounds";
include "include/client";
include "include/sounds";
include ":damage:damage";

program SpellScript_Earthquake(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	// Not sure if earthquake has a GFX
	// PlayObjectCenteredEffect(targ, GFX_FIRE_COLUMN, 7, 7);
	PlaySoundEffect(mobile, SFX_SPELL_EARTHQUAKE);

	var party := GetObjProperty(mobile, "#Party");
	var base_damage;
	var damage;
	
	// We'll use 15 for the range, which is slightly less than the visible screen
	foreach targ in ListMobilesNearLocation(mobile.x, mobile.y, mobile.z, 15, mobile.realm)
		if ( CheckLineOfSight(mobile, targ) && !(targ.serial in party) && (mobile.serial != targ.serial) )
			base_damage := (CDbl(AP_GetVital(targ, HITS)) / 3);
			damage := MS_GetSpellDamage(mobile, targ, base_damage);
			
			// Min and max damage for NPCs is 10 and 100
			if ( targ.npctemplate )
				if ( damage < 10 )
					damage := 10;
				elseif ( damage > 100 )
					damage := 100;
				endif
			endif
			
			ApplyDamageEX(targ, damage, info.damage_type, mobile);
		endif
	endforeach

	return 1;
endprogram
