use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";
include ":damage:damage";

program SpellScript_Earthquake(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	MS_PlaySpellSFX(info.spell_id, mobile);

	var party := GetObjProperty(mobile, "#Party");
	var base_damage;
	var damage;
	var area_size := CInt(GetConfigInt(info.spell_config, "AreaSize"));
	foreach targ in ListMobilesNearLocation(mobile.x, mobile.y, mobile.z, area_size, mobile.realm)
		if ( CheckLineOfSight(mobile, targ) && !(targ.serial in party) && (mobile.serial != targ.serial) )
			base_damage := (CDbl(AP_GetVital(targ, HITS)) / 3);
			damage := MS_GetSpellDamage(mobile, targ, base_damage);
			
			// Min and max damage for NPCs is 10 and 100
			if ( targ.npctemplate )
				if ( damage < 10 )
					damage := 10;
				elseif ( damage > 100 )
					damage := 100;
				endif
			endif

			MS_PlaySpellGFX(info.spell_id, targ, mobile);
			ApplyDamageEX(targ, damage, info.damage_type, mobile);
		endif
	endforeach

	return 1;
endprogram
