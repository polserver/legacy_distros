// $Id$

use uo;
use os;

include "include/client";
include ":magery:spells";
include ":magery:fields";

program SpellScript_WallOfStone(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	MS_PlaySpellSFX(info.spell_id, targ);

	var direction := GetDirectionByFacing(mobile);
	var objtype;
	if ( direction == FIELD_SHAPE_HORIZONTAL )
		objtype := 0x58;
	elseif ( direction == FIELD_SHAPE_VERTICAL )
		objtype := 0x57;
	else
		objtype := 0x80;
	endif

	var size := GetConfigInt(info.spell_config, "AreaSize");
	var coordinates := GetCoordinatesForField(targ.x, targ.y, direction, CInt(size/2));
	var item_list := array{};

	foreach coord in ( coordinates )
		var z := targ.z;
		if ( z < GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm) )
			z := GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm);
		endif

		var mobiles := ListMobilesNearLocationEx(coord.x, coord.y, z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, mobile.realm);
		if (  mobiles.Size() > 0 )
			continue; // Dont create walls on top of mobiles.
		elseif ( !CheckLosAt(mobile, coord.x, coord.y, z ) )
			continue; // Only create a field piece if the caster has line of sight.
		endif

		var item := CreateItemAtLocation(coord.x, coord.y, z, objtype, 1, mobile.realm);
		if ( item != error )
			item_list.Append(item);
		endif

		SleepMS(2);
	endforeach

	Sleep(10);

	foreach item in ( item_list )
		PlayStationaryEffect(item.x, item.y, item.z, GFX_SMOKE, 10, 10, 0, item.realm);
		DestroyItem(item);
		SleepMS(2);
	endforeach

	return 1;
endprogram
