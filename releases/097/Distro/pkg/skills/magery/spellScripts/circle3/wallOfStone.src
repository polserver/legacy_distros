use uo;
use os;

include "include/client";
include "include/facings";
include ":magery:spells";
include ":magery:spellSounds";
include ":magery:fields";

program SpellScript_WallOfStone(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;
	
	var targ := MS_TargetCoordinates(mobile, info.targ, "Select a target.");

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif
	
	MS_PlaySpellSFX(info.spell_id, targ);
	
	// Duration: 10 seconds
	var duration := 10;
	var gfx := CInt(GetConfigInt(info.spell_config, "GFX"));
	var gfx2 := CInt(GetConfigInt(info.spell_config, "GFX2"));
	var area_size := CInt(GetConfigInt(info.spell_config, "AreaSize"));
	if ( targ.facing == 1 || targ.facing == 2 || targ.facing == 5 || targ.facing == 6 )
		CreateField(targ, FIELD_SHAPE_HORIZONTAL, gfx, area_size, duration, "", mobile, 1);
	else
		CreateField(targ, FIELD_SHAPE_VERTICAL, gfx2, area_size, duration, "", mobile, 1);
	endif
	
	return 1;
endprogram
