use uo;
use os;

include ":attributes:attributes";
include ":magery:spells";
include ":magery:spellSounds";
include "include/client";
include "include/sounds";
include ":damage:damage";

program SpellScript_FlameStrike(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS + TGTOPT_HARMFUL);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	PlayObjectCenteredEffect(targ, GFX_FIRE_COLUMN, 7, 7);
	PlaySoundEffect(mobile, SFX_SPELL_FLAME_STRIKE);

	var base_damage := info.min_damage + (CDbl(RandomInt(CInt((info.max_damage - info.min_damage) * 10))) / 10);
	var damage := MS_GetSpellDamage(mobile, targ, base_damage);

	ApplyDamageEX(targ, damage, info.damage_type, mobile);

	return 1;
endprogram
