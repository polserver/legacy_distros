use uo;
use os;

include ":attributes:attributes";
include ":magery:spells";
include ":magery:spellSounds";
include "include/client";
include "include/sounds";
include ":damage:damage";

program SpellScript_Explosion(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	// Tell spell starter to not wait for this script to finish
	var spell_starter := GetProcess(info.pid);
	spell_starter.SendEvent("continue");

	Sleep(2);
	
	PlayObjectCenteredEffect(targ, GFX_EXPLOSION_BALL, 7, 7);
	PlaySoundEffect(mobile, SFX_SPELL_EXPLOSION);

	var base_damage := info.min_damage + (CDbl(RandomInt(CInt((info.max_damage - info.min_damage) * 10))) / 10);
	var damage := MS_GetSpellDamage(mobile, targ, base_damage);

	ApplyDamageEX(targ, damage, info.damage_type, mobile);
	
	return 1;
endprogram
