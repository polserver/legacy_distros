use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";
include ":timedscripts:timedScripts";

program SpellScript_MassCurse(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	MS_PlaySpellSFX(info.spell_id, targ);

	// Get all targets in area excluding party and self.
	var area_size := CInt(GetConfigInt(info.spell_config, "AreaSize"));
	var exclude := GetObjProperty(mobile, "#Party");
	exclude.Append(mobile.serial);
	var targs := MS_GetVaildTargets(targ, area_size, 1, exclude);
	
	// Duration: 120% Magery
	var duration := CInt(AP_GetSkill(mobile, MAGERY) * 1.2);

	foreach vtarg in targs
		MS_PlaySpellGFX(info.spell_id, vtarg, mobile);
		TS_StartTimer(vtarg, "Curse", duration, 0, mobile);
	endforeach
	
	return 1;
endprogram
