/* $Id
 *
 */

use uo;
use os;

include ":attributes:attributes";
include "include/client";

program skill_Stealth( who )
	if( AP_GetSkill(who, HIDING) < 80 )
		PrintTextAbovePrivate(who, "You are not good enough at hiding to do that.", who);
		return 0;
	elseif( (!who.hidden) && (AP_GetSkill(who, STEALTH) < 80) )
		PrintTextAbovePrivate(who, "You must be hidden to use stealth.", who);
		return 0;
	elseif( GetEquipmentByLayer(who, LAYER_MOUNT ) )
		who.hidden := 0;
		PrintTextAbovePrivate(who, "You can't stealth while riding a horse.", who);
		return 0;
	endif

	if( SkillCheck(who, STEALTH, -1) > 0 )
		if( !who.hidden )
			// Used RSTC because it runs the script in critical mode, thus
			// ensuring the hiding attempt completes before the stealth
			// script continues.
			var script := Run_Script_To_Completion(":hiding:hiding", {who, STEALTH});
			if ( script.errortext )
				SendSysMessage(who, "Error starting hiding - "+script.errortext);
			endif			
			
			if ( !who.hidden )
				SendSysMessage(who, "You must be hidden to stealth.");
				return 0;
			endif
		endif
		
		var my_Skill := AP_GetSkill(who, STEALTH) / 10;
		who.stealthsteps := CInt(my_Skill + RandomInt(my_Skill));
		PrintTextAbovePrivate(who, "You are now moving stealthily.", who);
		
		return 1;
	else
		if( who.hidden )
			who.hidden := 0;
			PrintTextAbovePrivate(who, "You have revealed yourself!", who);
		else
			PrintTextAbovePrivate(who, "You fail to move stealthily!", who);
		endif
		
		return 0;
	endif
endprogram
