/*
 * $Id: greaterHeal.src 319 2006-06-12 19:51:26Z austinheilman $
 *
 */

use uo;
use os;

include ":attributes:attributes";
include ":magery:spells";
include ":magery:spellSounds";
include "include/client";
include "include/sounds";

program SpellScript_GreaterHeal(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;
	
	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	// Amount healed: 40% Magery + (1 to 10)
	var points := CInt(AP_GetSkill(mobile, MAGERY) * 0.4) + RandomDiceRoll("1d10");
	
	PlayObjectCenteredEffect(targ, GFX_BLUE_SPARKLE_SWIRL, 7, 16);
	PlaySoundEffect(targ, SFX_SPELL_GREATER_HEAL);

	HealDamage(targ, points);
	
	SendSysMessage(mobile, "You healed "+points+" hit points of damage.");
	
	if ( mobile.serial != targ.serial )
		SendSysMessage(targ, "You have had "+points+" hit points of damage healed.");
	endif
	
	return 1;
endprogram
