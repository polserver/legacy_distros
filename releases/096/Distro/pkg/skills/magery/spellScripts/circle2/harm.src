use uo;
use os;

include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":magery:spellSounds";
include ":magery:spells";
include "include/client";
include "include/sounds";
include ":damage:damage";

program SpellScript_Harm(params)
	var mobile := params[1];
	var info := params[2];
	var spell_id := params[3];
	params := 0;

	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	PlaySoundEffect(mobile, SFX_SPELL_HARM);
	PlayObjectCenteredEffect(mobile, GFX_RED_SPARKLES, 7, 16);

	// Base damage: 5.2 - 6.6
	var dmg_info := MS_GetSpellDamage(mobile, spell_id);
	if ( dmg_info == error )
		return 0;
	endif

	// 1 tile = full damage, 2 tiles = half damage, >3 tiles = 1/4 damage
	case ( Distance(mobile, targ) )
		1: ApplyDamageEX(targ, dmg_info.damage, dmg_info.type, mobile);
		2: ApplyDamageEX(targ, CInt(dmg_info.damage / 2), dmg_info.type, mobile);
		default: ApplyDamageEX(targ, CInt(dmg_info.damage / 4), dmg_info.type, mobile);
	endcase

	return 1;
endprogram
