/*
        Maintened by *Edwards
        
        2009-09-06

        Last Modifications 2009-12-21
             * Added check if player hidden or NPC to return 0

        Last Modifications 2010-09-26
        
        Thanks to Gm Gates and Zulu for this wonderfull script :_)
*/
use uo;
use os;
use util;
use cfgfile;

include ":brainAI:eventid";
include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcUtil";
include ":attributes:attributes";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":attributes:titles";

program BrainNerve( params )

	var npc		:= params[1];
	var event	:= params[3];
	params := 0; // Not needed anymore.

	while( npc )
		if( event )
			case( event.type )
				SYSEVENT_SPEECH:
					SpeechEvent( npc, event );
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		SleepMS(5);
		event := Wait_For_Event(1);
	endwhile
 
        return 1;
endprogram

function SpeechEvent( npc, byref event )

        if( event.source.hidden || event.source.IsA( POLCLASS_NPC ))
		return 1;
        endif

	AI_Turn( npc, event.source, NETURN_TOWARD );
 
        var text := Lower( event.text );
        if( text["help"] )
                AI_Speak( npc, "I will obey to the following orders:" );
                Sleep(3);
                AI_Speak( npc, "title" );
	elseif( text["title"] )
		SendTitles( npc, event.source );
		return 1;
	endif

	return 1;
endfunction

function SendTitles( npc, mobile )

        var gump := GFCreateGump();

        //Load Main Background
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 300, 425 );

        GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 270, 25 );
	GFTextMid( gump, 15, 17, 300, 1720, "Select your Title" );

        //BackgroundAreas
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 270, 370 );
        GFTextMid( gump, 15, 60, 270, 1720, "Menu" );

        var skill_list := AP_ListAttributesByType( "skill" ),
	    num := skill_list.Size();

	var counter := 0,
	    y_pos := 100;

	GFPage( gump, 1 );
	foreach skill in ( skill_list )

                var title := AP_GetSkillTitle( skill ),
                    level := AP_GetLevelTitle( CDbl( AP_GetSkill( mobile, skill )));

                if( !level )
			continue;
                endif

                GFTextLine( gump, 50, y_pos, 2100, level+" "+title );
                GFAddButton( gump, 30, y_pos+3, 2117, 2118, GF_CLOSE_BTN, _skill_iter );

		y_pos += 20;
		counter += 1;

		if( counter >= 13 && _skill_iter < num )
			GFAddButton( gump, 30, 385, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump, gump.cur_page+1 );
			GFAddButton( gump, 30, 55, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			counter := 0;
			y_pos := 100;
		endif
		SleepMS(2);
	endforeach

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( !input )
                AI_Speak( npc, "Your title has been removed." );
		mobile.title_suffix := "";
		return 0;
        endif

        input := input[0];
        var result := skill_list[input];

        var title := AP_GetSkillTitle( result ),
            level := AP_GetLevelTitle( CDbl( AP_GetSkill( mobile, result )));

        mobile.title_suffix := ", "+level+" "+title;
        
        AI_Speak( npc, "Your title has been updated." );
        
	return 1;
endfunction
