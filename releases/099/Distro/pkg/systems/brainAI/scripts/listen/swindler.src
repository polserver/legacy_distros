/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2010-08-28

        Last Modifications 2010-09-26

        Idea from Loony the magic. He took it from Diablo. Well, it's now UO feature.. :)
*/
use uo;
use os;
use util;
use cfgfile;

include ":brainAI:eventid";
include ":brainAI:merchantUtil";
include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcUtil";
include ":merchants:swindler";
include ":merchants:storage";

program BrainNerve( params )

	var npc		:= params[1];
	//var nerve_name:= params[2];
	var event	:= params[3];
	//var settings	:= params[4];
	//var scripts	:= params[5];
	params := 0; // Not needed anymore.

	var forsale := CPM_GetMerchantContainer( npc, CPM_FORSALE, CP_CREATE );
     
	while( npc )
		if( event )
			case( event.type )
				SYSEVENT_SPEECH:
					SpeechEvent( npc, event, forsale );
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		SleepMS(5);
		event := Wait_For_Event(1);
	endwhile
 
        return 1;
endprogram

function SpeechEvent( npc, byref event, forsale )

        if( event.source.hidden || event.source.IsA( POLCLASS_NPC ))
		return 1;
        endif
        
        var shopping_cart := Swindler_Check_Restock( npc, forsale );

	AI_Turn( npc, event.source, NETURN_TOWARD );
        
        var text := Lower( event.text );
        if( text["help"] )
                AI_Speak( npc, "I will obey to the following orders:" );
                Sleep(3);
                AI_Speak( npc, "buy" );
	elseif( text["buy"] )
		BuyStuff( npc, event.source, forsale, shopping_cart );
	endif

	return 1;
endfunction

function BuyStuff( npc, mobile, forsale, shopping_cart )

	var result := SendBuyWindow( mobile, forsale, npc, shopping_cart, VENDOR_SEND_AOS_TOOLTIP );
	if( result.errortext )
		PrintTextAbove( npc, "SendBuyWindow() error - "+result.errortext );
	endif

	return 1;
endfunction
