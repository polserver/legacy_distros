/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2009-10-26
*/
use uo;
use util;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":lottery:lottery";

program UseTicket( mobile, ticket )

        if( !GetObjProperty( ticket, "TicketNum" ))
                SetupTicket( ticket );
        endif

        var gump := GFCreateGump();

        //Main Background
  	GFGumpPic( gump, 83, 63, 0x820 );
  	GFGumpPic( gump, 100, 100, 0x821 );
  	GFGumpPic( gump, 100, 170, 0x822 );
  	GFGumpPic( gump, 100, 240, 0x823 );

        //Top/Bottom Deco
  	GFGumpPic( gump, 120, 100, 0x82b );
  	GFGumpPic( gump, 120, 225, 0x82b );
   
        var ticket_num := CStr( GetObjProperty( ticket, "TicketNum" ));
        if( ticket_num.errortext )
                SendSysMessage( mobile, "Error reading ticket number -->"+ticket_num.errortext );
                return 0;
        endif
        
        GFTextMid( gump, 120, 120, 250, 2100, "Ticket Lottery #"+Loto_GetLottery() );
        GFTextMid( gump, 130, 160, 250, 2100, ticket_num );

	GFSendGump( mobile, gump );

	return 1;
endprogram

function SetupTicket( ticket )

        var cur_lottery := Loto_GetLottery();
        var new_ticket := Loto_NewTicket();

        var tickets := Loto_GetTickets( cur_lottery );
        while( new_ticket in tickets )
                //Find a new combination if already in scope...
                new_ticket := Loto_NewTicket();
                SleepMS(5);
        endwhile

        tickets.Append( new_ticket );
        Loto_SetTickets( cur_lottery, tickets );
        Loto_AddLotteryPrize( Loto_GetLottery(), 5000 );

        SetObjProperty( ticket, "TicketNum", new_ticket );
        SetObjProperty( ticket, "Lottery", cur_lottery );

	return 1;
endfunction
