use uo;
use os;
include "include/attributes";

program demod(parms)
  var mod := lower(parms[1]);
  var who := parms[2];
  var amt := parms[3];
  var duration := parms[4];
  var serial := parms[5];
  sleep(duration);
  undo_mod(who, mod, amt, serial);
endprogram

function undo_mod(who, mod, amt, serial)
  var dexpenamt := 0;
  if(!who)
    who := SystemFindObjectBySerial(serial, SYSFIND_SEARCH_OFFLINE_MOBILES);
  endif
  case(mod)
    "str": if((GetStrengthMod(who) - amt) < 0)
             SetStrengthMod(who, 0);
           else
             SetStrengthMod(who, GetStrengthMod(who) - amt);
           endif
    "dex": if((GetDexterityMod(who) - amt) < 0)
             dexpenamt := dexPen(who);
             SetDexterityMod(who, (0 - dexpenamt));
           else
             SetDexterityMod(who, GetDexterityMod(who) - amt);
           endif   
    "int": if((GetIntelligenceMod(who) - amt) < 0)
             SetIntelligenceMod(who, 0);
           else
             SetIntelligenceMod(who, GetIntelligenceMod(who) - amt);
           endif
//****** Important fix (0 ar bug) for protecion clothes ******
    "ar":  if (who.ar_mod - amt > 0)
                  who.ar_mod := who.ar_mod - amt;
           else
                  who.ar_mod := 0;
           endif
//****** End ******
    "p" :  who.setParalyzed(0);
  endcase
  EraseModFlags(who, mod);
  PlaySoundEffect(who, 0x01eb);
  PlayObjectCenteredEffect(who, 0x373a, 10,10);
endfunction

function EraseModFlags(who, stat)
  var mod;
  case(stat)
    "str": mod := "#ss";
    "dex" : mod := "#dd";
    "int" : mod := "#ii";
    "ar"  : mod := "#ar";
    "p"   : mod := "#pp";
  endcase
  if(who)
    EraseObjProperty(who, mod);
  endif
endfunction

Function dexPen(me)

var myarmor := ListEquippedItems( me );
var itemdesc := ReadConfigFile(":combat:itemdesc");
var penalty := 0;

foreach item in myarmor
  var penamt := CInt(itemdesc[item.objtype].DexPenalty);
  if(penamt)
    penalty  := penalty + penamt;
  endif
endforeach

if(me.cmdlevel)
  SendSysMessage(me, "AR Penalty Total: " + penalty + ".");
endif

Return penalty;

Endfunction
