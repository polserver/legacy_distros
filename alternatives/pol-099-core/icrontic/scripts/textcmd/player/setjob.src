///////
// setjob.src - allows user to pick new skill title from his/her 7 highest skills
//
// Author: Racalac
// Date: August 15, 2000
// Revision: 2.0 by MuadDib
//
//
// Notes: Only the 7 highest because a normal skillcap is 700, letting a user
// 		pick from one of his 7xGM skills (sic)
//		Problems could arise by letting the player choose any of his skills
//		to be his profession. A leet PKer could fool people by having "Neophyte
//		Beggar" on his paperdoll...
//
//
/////////////////////////////////////////////////////////////
 

use basic;
use uo;
use cfgfile;

include "include/client";
include "include/attributes";

var highskillz, who, newtitle;;
var skillscfg := ReadConfigFile(":*:skills");
var titles := {};
// Title ID added for setjob override in titleCheck.src in foundations
var titleid := {};
var layout := {

"page 0",
"noclose",
"nodispose",
"gumppic 0   50  2620",
"gumppic 20  50  2621",
"gumppic 130  50  2621",
"gumppic 390 50  2622",

"gumppic 0   68  2623",
"gumppic 22  68  2624",
"gumppic 125 68  2624",
"gumppic 388 68  2625",

"gumppic 0   272 2626",
"gumppic 20  272 2627",
"gumppic 130  272 2627",
"gumppic 390 272 2628",

"page 1",
"text 10 60 40 0",
"text 10 100 40 1",
"button 300 98 242 241 1 1 0",
};

var data := {

"Current Skill Title:",
"Pick New Skill Title:",
};

program titlechooser(user, text )
  if(text == "clear")
    EraseObjProperty(who, "setjob");
    return;
  endif
        who := user;
        var skillid;
	FillInArrays();
	var ret := SendDialogGump( who, layout, data );
	if(ret[0] = 0)
		return;
	endif
	SendSysmessage(who,"Setting professional title to " + titles[ret[0]]);
	who.title_suffix := ", " + titles[ret[0]];
        SetObjProperty(who, "setjob", titleid[ret[0]]);
endprogram

function FillInArrays()
	var prev_y := 110;
	var buttonret := 0;
	var realtitle;
        
	if(!who.title_suffix)
		realtitle := "<none>";
	else
	//This script prepends the suffix title with ", " for the paperdoll's sale,
	// so we strip that out when showing it on this gump.
		var space := find(who.title_suffix," ",1);
		realtitle := who.title_suffix[space+1,len(who.title_suffix)];		
	endif
	
	data.append(realtitle);
	layout.append("text 170 60 40 " + cstr(len(data)-1) );
	
	highskillz := HighSkills(who);
	
	foreach skillid in highskillz
	  if(skillid != -1)
		layout.append("button 10 " + cstr(prev_y + 20) + " 2640 2641 1 0 " + (buttonret+1));
		data.append(skillscfg[skillid].name);
		layout.append("text 40 " + cstr(prev_y + 20) + " 40 " + cstr(len(data)-1));
		newtitle := ProfLevel(skill(skillid)) + " " + skillscfg[skillid].Title;
		data.append(newtitle);
                titleid.append(skillscfg[skillid].SkillId);
		titles.append(newtitle);
		layout.append("text 200 " + cstr(prev_y + 20) + " 40 " + cstr(len(data)-1));
		prev_y := prev_y + 20;
		buttonret := buttonret + 1;
	  endif
	endforeach

endfunction

function HighSkills(who)
	var s1 := -1;
	var s2 := -1;
	var s3 := -1;
	var s4 := -1;
	var s5 := -1;
	var s6 := -1;
	var s7 := -1;
	
	var i;
	
	for(i := 0;i <= SKILLID__HIGHEST; i := i + 1)
	  if(skill(i) >= 0)
		if(skill(i) > skill(s1))
			s7 := s6;
			s6 := s5;
			s5 := s4;
			s4 := s3;
			s3 := s2;
			s2 := s1;
			s1 := i;
		elseif(skill(i) > skill(s2))
			s7 := s6;
			s6 := s5;
			s5 := s4;
			s4 := s3;
			s3 := s2;
			s2 := i;
		elseif(skill(i) > skill(s3))
			s7 := s6;
			s6 := s5;
			s5 := s4;
			s4 := s3;
			s3 := i;
		elseif(skill(i) > skill(s4))
			s7 := s6;
			s6 := s5;
			s5 := s4;
			s4 := i;		
		elseif(skill(i) > skill(s5))
			s7 := s6;
			s6 := s5;
			s5 := i;				
		elseif(skill(i) > skill(s6))
			s7 := s6;
			s6 := i;				
		elseif(skill(i) > skill(s7))
			s7 := i;
		endif
	  endif
	endfor
	
	var ret := {s1,s2,s3,s4,s5,s6,s7};
	return ret;
	
endfunction

function skill(skillid)
	return GetBaseSkillBaseValue(who, skillid);
endfunction
	
function ProfLevel( value )

	var ret;
	if( value >= 1000 )
		ret := "Grandmaster";
	elseif( value >= 900 )
		ret := "Master";
	elseif( value >= 800 )
		ret := "Adept";
	elseif( value >= 700 )
		ret := "Expert";
	elseif( value >= 600 )
		ret := "Journeyman";
	elseif( value >= 500 )
		ret := "Apprentice";
	elseif( value >= 400 )
		ret := "Novice";
	else
		ret := "Neophyte";
	endif

	return ret;
	
endfunction
