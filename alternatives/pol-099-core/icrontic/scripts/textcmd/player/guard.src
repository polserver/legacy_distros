use os;
use uo;
use npc;
use util;
use cfgfile;

include "include/eventid";
var cfg := ReadConfigFile("::npcdesc");
var gzone := ReadConfigFile("::gzone");



program txtcmd_guards(who)
	if(who.dead)
		SendSysMessage(who, "You are dead, how can you yell for the guards??");
		return 0;
	endif
	if(who.criminal)
		SendSysMessage(who, "What is wrong with you! If the guards hear you, they'll kill you!");
		return 0;
	endif
	if(!CheckGuarded(who));
		SendSysMessage(who ,"The guards can't protect you here.");
		return 0;
	endif
	if(!lookaround(who))
		var timer:=GetObjProperty(who, "falarms");
		if(timer<readgameclock())
			timer:=readgameclock();
		endif
		SetObjProperty(who,"falarms",timer+20);
		if((timer-readgameclock())>60)
                        who.criminal := 1;
			SendSysMessage(who, "Now you've done it.");
			SetObjProperty(who, "called", 1);
			var guard := CreateNpcFromTemplate( "guard" , who.x, who.y, who.z );
			printtextabove(guard, who.name + ", I'll teach you not to cry wolf!");
			SetObjProperty(guard, "target", who.serial);
		elseif((timer-readgameclock())>40)
			SendSysMessage(who, "Are you sure you want to offend us?");
		elseif((timer-readgameclock())>20)
			SendSysMessage(who, "We warned you before.");
		else
			SendSysMessage(who, "The guards don't appreciate false alarms.");
		endif
	endif
endprogram

function CheckGuarded(who)
	var entries := GetConfigStringKeys(gzone);
	var element;
	var range;
	var guardzone := 0;
	foreach listing in entries
                sleepms(2);
		range := SplitWords(gzone[listing].range);
		if((who.x >= CInt(range[1])) && (who.x <= CInt(range[3])) && (who.y >= CInt(range[2])) && (who.y <= CInt(range[4])))
			if (gzone[listing].Guarded)
				guardzone := 1;
			else
				guardzone := 0;
			endif
		endif
	endforeach
	return guardzone;
endfunction

function LookAround(who)
        var me := who;
        var guard;
        var nearme := ListMobilesNearLocation(me.x, me.y, me.z, 15);
	foreach mobile in nearme
                if (CheckGuarded(mobile) == 1)
                sleepms(5);
		var elem := FindConfigElem(cfg, mobile.npctemplate);
		if (elem and elem.hostile and !elem.guardignore)
			if (mobile.script != "tamed")
				if (mobile.name)
                                        PrintTextAbove(who, "Guards! Help, I am being violated!");
					SetObjProperty(mobile, "called", 1);
    				        eraseobjproperty( who, "falarms");
					guard := CreateNpcFromTemplate( "guard" , mobile.x,mobile.y,mobile.z );
					printtextabove(guard, "I banish " + mobile.name + " to eternal darkness!");
					SetObjProperty(guard, "target", mobile.serial);
                                        return 1;
                                 endif
                        endif
                else
	            if(CheckForCriminals (mobile))
                      return 1;
                    endif
		endif
                endif
	endforeach
endfunction

function CheckForCriminals (byref jerk)
        var mobile := jerk;
        var guard;
	if (jerk.dead)
		return;
	endif
        if (jerk.criminal)
          if (CheckGuarded(mobile) == 1)
	    SetObjProperty(mobile, "called", 1);
	    guard := CreateNpcFromTemplate( "guard" , mobile.x,mobile.y,mobile.z );
	    printtextabove(guard, "Villan! " + mobile.name + ", you are not welcome here!");
	    SetObjProperty(guard, "target", mobile.serial);
            return 1;
          endif
        endif
endfunction
