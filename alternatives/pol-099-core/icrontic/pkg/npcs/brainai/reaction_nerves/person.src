///////////////////////////////////////////////
// person.src
//
// Created 2/13/2003
// Updated 2/13/2003
//
// By MuadDib
// http://www.icrontic.net
//
///////////////////////////////////////////////
//
// This is a simple reaction nerve that listens for
// entered area, engaged, damaged, and left area.
//
// This script is always running so long as the npc is alive
// but sleeps until it gets an event.
//
///////////////////////////////////////////////

use uo;
use os;

include "include/bank";
include "../ai_include/NPC_Commands";

program ReactionNerve(params)
	var nerve_name	:= params[1];
	var npc		:= params[2];
	var ev		:= params[3];
	var settings	:= params[4];
		
	while( npc )
		if ( ev )
			case ( ev.type )
				SYSEVENT_ENTEREDAREA:
					var source := ev.source;
					if ( source.npctemplate );
						//Do nothing
					elseif ( source.cmdlevel );
                                                AI_Turn(npc, source, NETURN_TOWARD);
                                                AI_Speak(npc, "All hail " + source.name);
					elseif ( CheckLineOfSight(npc, source) and source.ip )
                                                AI_Speak(npc, "Tis a good day is it not?");
					endif
                                SYSEVENT_ENGAGED:
                                        var source := ev.source;
                                        AI_Speak(npc, "Aaahhh! Help! Help! I'm being oppressed!");
                                        AI_Flee(npc, source);
                                SYSEVENT_DAMAGED:
                                        var source := ev.source;
                                        AI_Speak(npc, "Owie! That's worse than a bee sting!" );
                                        if (source)
                                          AI_Flee(npc, source);
                                        else
                                          AI_Speak(npc, "What sinister force is this!");
                                        endif
			endcase
		endif
		
		ev := wait_for_event(900);
	endwhile
endprogram
