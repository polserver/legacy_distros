///////////////////////////////////////////////
// personflee.src
//
// Created 7/26/2002 11:31AM
// Updated 7/26/2002 11:31AM
//
// By MuadDib
// http://www.icrontic.net
//
///////////////////////////////////////////////
//
// This nerve causes a creature to run
// away from what ever is attacking it.
//
///////////////////////////////////////////////

use uo;
use os;
use npc;

include "../ai_include/NPC_Commands";

program FleeNerve(params)
	var nerve_name	:= params[1];
	var npc		:= params[2];
	var opponent	:= params[3].source;
	var settings	:= params[4];

	AI_Setopponent(npc, opponent);

	//Store it here so it does not have to do the math over and over.
	var flee_level := CInt((GetVitalMaximumValue(npc, "hits") * settings["flee_level"]) / 100);

	while( npc )
               var dist := Distance(npc, opponent);
		
               DisableEvents( SYSEVENT_ENTEREDAREA + SYSEVENT_LEFTAREA );
               var waittime;
               outer:
               while (opponent && !opponent.dead)
                 waittime := OpenDistance(npc, opponent ) * 15;
                 var ev := wait_for_event( waittime );
                 case (ev.type)
                     SYSEVENT_SPEECH:
                     SYSEVENT_DISENGAGED:     if (ev.source.serial == opponent.serial)
                                                AI_Speak(npc, "Whew!" );
                                                break outer;
                                              endif
                     SYSEVENT_ENGAGED:
                     SYSEVENT_DAMAGED:        if (ev.source)
                                                if (ev.source.serial != opponent.serial)
                                                  AI_Speak(npc, "Ack! Leave me alone!" );
                                                else
                                                  if (ev.type == "damage")
                                                    AI_Speak(npc, "Ouch! Heeelp!");
                                                  endif
                                                endif
                                              endif
                     SYSEVENT_OPPONENT_MOVED:
                     SYSEVENT_ENTEREDAREA:
                     SYSEVENT_LEFTAREA:
                 endcase
               endwhile
               EnableEvents( SYSEVENT_ENTEREDAREA + SYSEVENT_LEFTAREA);
		//If distance is over 20 from opponent, and no new one disengage and reset
			if ( !opponent )
				var new_opponent := FleeStatus(npc, opponent, nerve_name);
				if ( ! new_opponent )
					AI_Disengage(npc);
					AI_EndNerve(npc, nerve_name);
				else
					opponent := new_opponent;
				endif
			elseif ( dist > 20 )
				var new_opponent := FleeStatus(npc, opponent, nerve_name);
				if ( new_opponent )
					opponent := new_opponent;
				else
					AI_Disengage(npc);
					AI_EndNerve(npc, nerve_name);
				endif
			endif

               sleepms(600);
               wait_for_event(0);
	endwhile

endprogram

function OpenDistance(npc, opponent )
  case Distance( npc, opponent )
    10:      return 1;
    default: RunAwayFrom( opponent );
             return 0;
  endcase
endfunction

function FleeStatus(npc, byref opponent, nerve_name)

	var hostiles := ListHostiles(npc, 10);
	if ( hostiles.size() > 0 )
		//Return the first hostile in the list to run from.
		//It is usually the closest one.
		AI_Setopponent(npc, hostiles[1]);
		return hostiles[1];
	else
		//If there are no hostiles return 0.
		return 0;
	endif

	return 0;
endfunction
