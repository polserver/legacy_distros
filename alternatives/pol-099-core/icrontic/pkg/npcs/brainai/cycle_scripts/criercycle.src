///////////////////////////////////////////////
// criercycle.src
//
// Created 2/11/2003
// Updated 2/11/2003
//
// By MuadDib
// http://www.icrontic.net
//
///////////////////////////////////////////////
//
// This is not a nerve, but a script that is run
// critically every cycle (brain loop) if the npc is 
// awake.
//
// It can be used for various things but is most
// useful on hostile npcs to look around for players
// especially when joined with a reaction script that
// wakes the npc up and makes this run.
///////////////////////////////////////////////
//
// This script requires an AI_Setting in the
// npcdesc.cfg for attack_range which specifies
// how many spaces around the npc should look.
// 
// If the attack_range is 0, it sets it to 5.
//
// Crier Cycle based from targetpcs.src
///////////////////////////////////////////////

use uo;
use os;
use util;

include "../ai_include/NPC_Commands";
include "../ai_include/NPC_Nerves";

program CycleScript(params)
	var npc		:= params[1];
	var settings	:= params[2];
	
	//PrintTextAbove(npc, "*looks around*");
	var nerves := LoadNerves(npc);
	if ( (nerves.exists("Combat")) || (nerves.exists("Flee")) )
		//If the npc is fleeing or in combat, there is no
		//need to run this script any further.
		return 0;
	endif
	
	var range := settings["attack_range"];
	if ( !range )
		range := 5;
	endif
	
	var speakto_list := array;
	foreach mobile in ListMobilesInLineOfSight(npc, range)
		//Only make a list of 'online' players who do not have
		//a command level.
		if ( (mobile.ip) && (!mobile.cmdlevel) )
			speakto_list.append(mobile);
		endif
		sleepms(2);
	endforeach
	
	//From the mobile list, pick one to personally speak too
	if ( speakto_list.size() > 0 )
		var spokento := speakto_list[RandomInt(speakto_list.size())+1];
		AI_Speak(npc, whatToSay(spokento));
		return 1;
	else
		return 0;
	endif
endprogram

function whatToSay(spokento)
  var sayit;
  var news := GetGlobalProperty("news");
  if(!news)
    // No news is set
    news := ("No news is good news.");
  else
    // Report there IS news to be given if asked
    news := ("Thy lord has recieved new news.");
  endif
  sleepms(100);
  // Return a personal greeting with news status
  sayit := "Hail " + spokento.name + "! " + news;
  return CStr(sayit);
  
endfunction
