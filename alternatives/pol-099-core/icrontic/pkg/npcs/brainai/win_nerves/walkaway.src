///////////////////////////////////////////////
// walkaway.src
//
// Created 7/26/2002 11:31AM
// Updated 7/26/2002 11:31AM
//
// By Austin Heilman
// http://www.tsse.net
//
///////////////////////////////////////////////
//
// This nerve makes an npc walk away from the
// corpse of a player or what ever else it killed.
// It does require chrdeath.src to store a cprop called
// "corpse_serial" on the ghost or if you do this but
// with another cprop name, then change the cprop it 
// looks for here.
//
// The npc will also look around for other players
// too attack before walking away.
//
///////////////////////////////////////////////

use uo;
use util;
use os;
use math;

include "../ai_include/NPC_Commands";

program WinNerve(params)
	var nerve_name	:= params[1];
	var npc		:= params[2];
	var ghost	:= params[3].source;
	var settings	:= params[4];

	if ( !ghost )
		AI_EndNerve(npc, nerve_name);
		return;
	endif

	var hostiles;
	
	//Stop the combat nerve incase its still running
	AI_EndNerve(npc, "Combat");

	//Get the victim's corpse and its location
	var corpse := SystemFindObjectBySerial(GetObjProperty(ghost, "corpse_serial"));
	var corpse_x := corpse.x;
	var corpse_y := corpse.y;

	while ( CoordDistance(npc.x, npc.y, corpse_x, corpse_y) < CInt(settings["attack_range"])+5 )
		var new_opponent := LookAround(npc, settings["attack_range"]);
		if ( new_opponent )
			AI_Attack(npc, new_opponent);
			AI_EndNerve(npc, nerve_name);
			return;
		elseif ( !ghost.dead )
			AI_EndNerve(npc, nerve_name);
			return;
		elseif ( npc.warmode )
			AI_EndNerve(npc, nerve_name);
			return;
		else
			AI_Move_XYZ(npc, corpse_x, corpse_y, NEMOVE_AWAY, NEMOVE_WALK, NOWAKE, settings["attack_range"]);
		endif

		sleepms(600);

		while ( Events_Waiting() )
			var ev := wait_for_event(120);
		endwhile
	endwhile
	AI_EndNerve(npc, nerve_name);
endprogram

function LookAround(npc, range)
	var attack_list := array;
	var hostiles := ListHostiles(npc, 10);
	if ( hostiles.size() > 0 )
		return hostiles[1];
	endif

	foreach mobile in ListMobilesInLineOfSight(npc, range)
		if ( (mobile.ip) && (!mobile.cmdlevel) )
			attack_list.append(mobile);
		endif
		sleepms(2);
	endforeach

	if ( attack_list.size() > 0 )
		var victim := attack_list[RandomInt(attack_list.size())+1];
		return victim;
	else
		return 0;
	endif
endfunction

function CoordDistance(x1, y1 ,x2 ,y2)
	var x_dist := abs(x1 - x2);
	var y_dist := abs(y1 - y2);
	
	if ( x_dist > y_dist)
		return x_dist;
	else
		return y_dist;
	endif
endfunction