use uo;
use util;
use os;


include "include/client";
include "castopts";
include "include/attributes";

const PoisonSound := 0x1e2;
const PoisonEffect := 0x374a;

var spelldelay := 20;

program cast_plague(caster)

	var options := GetObjProperty(caster, "NecroOpts");
	if (!options)
		options := {};
	else
		options := splitwords(options);
	endif
	EraseObjProperty(caster, "NecroOpts");

	if (IsNoWords(options) == 0)
		SpeakPowerWords(caster, 76);
	endif

	if (IsNoDelay(options) == 0)
		While (spelldelay > 0)
			PerformAction(caster, ANIM_CAST_DIR);
			spelldelay := spelldelay - 1;
			sleep(1);
		endwhile
	endif

	if (IsNoMana(options) == 0)
		if (!ConsumeMana(caster, 76))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Mana.");
			return;
		endif
	endif

	if (IsNoRegs(options) == 0)
		if (!ConsumeReagents(caster, 76))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Reagents.");
			return;
		endif
	endif

	if (IsNoSkillCheck(options) == 0)
		if (!CheckSkill(caster, SKILLID_MAGERY, 100, 0))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			return;
		endif
	endif

	var CasterSkill   := GetEffectiveSkill(caster, SKILLID_MAGERY);
	var Range	  := Cint(CasterSkill / 2);
	var PoisonPower   := Cint(CasterSkill / 10);
	var PoisonLevel   := 0;
	var InitialDamage := 0;
        var passparms := { };

	var victims := ListMobilesNearLocation(caster.x, caster.y, caster.z, Range);

	foreach mobile in victims

		if ((mobile.serial != caster.serial) and (!mobile.dead))

			PlaySoundEffect(mobile, PoisonSound);
			PlayObjectCenteredEffect(mobile, PoisonEffect, 7, 0x10);

			SendSysMessage(mobile, "You feel deathly ill...");
			InitialDamage := RandomDiceRoll("6d6");
			ApplyRawDamage(mobile, InitialDamage);
			PoisonLevel := Cint(GetObjProperty(mobile, "poison_level"));
			PoisonLevel := PoisonLevel + PoisonPower;
			SetObjProperty(mobile, "poison_level", cstr(PoisonLevel));
                        passparms := { mobile, caster, "plague" };
			Start_Script(":spells:plagueDamage", passparms);
		endif

	endforeach
endprogram
