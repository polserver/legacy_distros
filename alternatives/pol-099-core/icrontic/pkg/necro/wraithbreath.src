use uo;
use os;
use util;

include "include/client";
include "castopts";
include "include/attributes";

const DrainEffect := 0x374a;
const DrainSound := 0x1fa;
const FlameStrike := 0x3709;

var spelldelay := 4;

program cast_wraithform(caster)

	var options := GetObjProperty(caster, "NecroOpts");
	if (!options)
		options := {};
	else
		options := splitwords(options);
	endif
	EraseObjProperty(caster, "NecroOpts");

	if (IsNoWords(options) == 0)
	        SpeakPowerWords(caster, 80);
	endif

	if (IsNoDelay(options) == 0)
		While (spelldelay > 0)
			PerformAction(caster, ANIM_CAST_DIR);
			spelldelay := spelldelay - 1;
			sleep(1);
		endwhile
		sleepms(500);
	endif

	if (IsNoMana(options) == 0)
	        if (!ConsumeMana(caster, 80))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Mana.");
			return;
		endif
	endif

	if (IsNoRegs(options) == 0)
	        if (!ConsumeReagents(caster, 80))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Reagents.");
			return;
		endif
	endif	

	if (IsNoSkillCheck(options) == 0)
	        if (!CheckSkill(caster, SKILLID_MAGERY, 90, 0))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			return;
		endif
	endif

	var CasterSkill := GetEffectiveSkill(caster, SKILLID_MAGERY);
	var Range := Cint (CasterSkill / 25);
	var PowerLevel := Cint(CasterSkill /10);
	var DamageDie := "2d" + PowerLevel;




	set_Critical(1);
	Detach();

		var targets := ListMobilesNearLocation(caster.x, caster.y, caster.z, Range);

		foreach mobile in targets
			if ((mobile.serial != caster.serial) and (mobile.dead = 0))
				var Dmg := RandomDiceRoll(DamageDie);
				PlayObjectCenteredEffect(mobile, DrainEffect, 7, 0x10);
				PlaySoundEffect(mobile, DrainSound);
				ApplyRawDamage(mobile, Dmg);
				caster.mp := caster.mp + Dmg;

				if (caster.mp > caster.maxmp)
					caster.mp := caster.maxmp;
				endif
			endif
		endforeach


endprogram
