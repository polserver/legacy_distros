use cfgfile;
use basic;
use os;
use uo;

include "include/eventid";
include "include/client";
include "include/myutil";
include "include/math";

program necrostaff(staff)

//temporary code:
	staff.name := "a lich staff";

	detach();

	RegisterForSpeechEvents( staff, 1 );
	EnableEvents( EVID_SPEECH, 1 );

	while (1)
		var ev := wait_for_event( 120 );
        	if (ev)
			if (IsEquipped(ev.source, staff))
            			if (ev.type = EVID_SPEECH)
					handle_speech( ev, staff );
				endif
			endif
        	endif
	endwhile

endprogram

function IsEquipped(me, staff)

	var weaponone := GetEquipmentByLayer( me,  LAYER_HAND1 );
	var weapontwo := GetEquipmentByLayer( me,  LAYER_HAND2 );

	if ( ( weaponone == staff ) or (weapontwo == staff ) )
		return 1;
	else
		//it isn't equipped, so fergetaboutit
		return 0;
	endif

endfunction

function handle_speech( event, staff )

	//a quick search to see if we need to bother looking for which spell to cast:
	var me := event.source;
	var text := lower( event.text );
	var powerwords := "dice corpus sine nomine expergefaceret nutu magistri supplicare animus ex corporis resolveretur enervare ulties manum necarent manes turbidi sollictique vocam manes sollicti mihi infundite nutu magistri se compellere manes turbidi sollictique resolverent umbrae tenebrae miserere animi non digna ferentis fluctus puter se aresceret umbrae tenebrae venarent nox nutu magistri se resolvere manes sollicti mi compellere";

	if ( powerwords[ text ] )
		//they said one of those words up there, anyway
		TryToCast(me,text);
		return;
	else
		//they didn't say any powerwords
		return;
	endif

endfunction

function TryToCast(me, powerwords)

	var spellcfg := readconfigfile("spells");
	var i, spellid, script;
	for (i := 65; i < 81; i := i + 1)
		if ( lower(spellcfg[i].PowerWords) = powerwords )
			spellid := i;
			script := spellcfg[i].Script;
			break;
		endif
	endfor

	if (!spellid)
		//what they said didn't matched the powerwords verbatim
		return;
	endif

	var tome;
	foreach item in ListRootItemsInContainerOfObjtype(me.backpack,0xA100)
		tome := item;
		break;
	endforeach

	if (!tome)
		return;
	endif

	//someone really like bitmap flag deallies so lets decypher this mess
	i := 1;
	var q := 8;
	var LTotal := 0;
	var GTotal := 0;
	var GreaterFlag := cint(GetObjProperty(tome, "Greater"));
	var LesserFlag := cint(GetObjProperty(tome, "Lesser"));
	var Options := GetObjProperty(tome, "Options");

	var LSpellArray := {78,65,66,67,68,69,80,77};
	var GSpellArray := {70,71,72,79,73,74,75,76};

	for ( i := 1; i < 9; i := i + 1 )
		if (FlagCheck(LesserFlag, i)=1)
			if (LSpellArray[i] = spellid)
				//oh, here it is:
				castspell(me,spellid,Options,script);
				return;
			endif
		endif
		if (FlagCheck(GreaterFlag, i)=1)
			if ( GSpellArray[i] = spellid )
				//ooh, it's a greater:
				castspell(me,spellid,Options,script);
				return;
			endif
		endif
	endfor

	//we didn't find the spell they cast in their book
	return;

endfunction

function CastSpell(me, spellid, options, script)

	//we just said the words, so lets cast without them:
	//other options are from the book (not the staff)

	if (!options)
		options := "";
	endif

	SetObjProperty(me,"NecroOpts",options + " NOWORDS");
	Set_Critical(1);
	Detach();
	StartSpellEFfect(me, spellid);
	Set_Critical(0);
endfunction
