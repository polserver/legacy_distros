use uo;
use os;
use cfgfile;

include "include/client";
include "castopts";

var spelldelay := 3;

program cast_controlundead(caster)
	
	var options := GetObjProperty(caster, "NecroOpts");
	if (!options)
		options := {};
	else
		options := splitwords(options);
	endif
	EraseObjProperty(caster, "NecroOpts");	

	var CasterSkill := GetAttribute(caster, "MAGERY");
	var duration := Cint(CasterSkill) * 5;

	if (IsNoWords(options) == 0)
		SpeakPowerWords(caster, 67);
	endif

	if (IsNoDelay(options) == 0)
		While (spelldelay > 0)
			PerformAction(caster, ANIM_CAST_DIR);
			spelldelay := spelldelay - 1;
			sleep(1);
		endwhile
		sleepms(500);
	endif

	if (IsNoMana(options) == 0)
		if (!ConsumeMana(caster, 67))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Mana.");
			return;
		endif
	endif	

	if (IsNoRegs(options) == 0)
		if (!ConsumeReagents(caster, 67))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Reagents.");
			return;
		endif
	endif

	if (IsNoSkillCheck(options) == 0)
		if (!CheckSkill(caster, SKILLID_MAGERY, 70, 0))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			return;
		endif
	endif

	SendSysMessage(caster, "Select target.");
	var victim := target(caster);

	if ( (!victim) or (victim.dead) )
		SendSysMessage(caster, "Aborted.");
		return;
	endif

        var npccfg  := ReadConfigFile("::npcdesc");
	var powerlevel := npccfg[victim.npctemplate].elemental;
	if ( (!powerlevel) or (powerlevel != "undead") )
		SendSysMessage(caster, "Invalid target.");
		return;
	endif

	detach();

	var oldscript := victim.script;
	SetObjProperty(victim, "master", caster.serial);
	victim.script := "tamed";
	RestartScript(victim);
	sleep(duration);
	victim.script := oldscript;
	RestartScript(victim);
	EraseObjProperty(victim, "master");

endprogram
