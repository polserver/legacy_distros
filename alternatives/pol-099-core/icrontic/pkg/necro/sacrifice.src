use uo;
use os;
use util;

include "include/client";
include "castopts";
include "include/attributes";

var spelldelay := 3;

program cast_raise_dead(caster)
    
	var options := GetObjProperty(caster, "NecroOpts");
	if (!options)
		options := {};
	else
		options := splitwords(options);
	endif
	EraseObjProperty(caster, "NecroOpts");

	if (IsNoWords(options) == 0)
		SpeakPowerWords(caster, 68);
	endif

	if (IsNoDelay(options) == 0)
		While (spelldelay > 0)
			PerformAction(caster, ANIM_CAST_DIR);
			spelldelay := spelldelay - 1;
			sleep(1);
		endwhile
		sleepms(500);
	endif


	if (IsNoMana(options) == 0)
	if (!ConsumeMana(caster, 68))
		PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
		PlaySoundEffect(caster, SFX_5B);
		SendSysMessage(caster, "Insufficient Mana.");
		return;
	endif
	endif

	if (IsNoRegs(options) == 0)
	if (!ConsumeReagents(caster, 68))
		PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
		PlaySoundEffect(caster, SFX_5B);
		SendSysMessage(caster, "Insufficient Reagents.");
		return;
	endif
	endif

	if (IsNoSkillCheck(options) == 0)
		if (!CheckSkill(caster, SKILLID_MAGERY, 70, 0))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			return;
		endif
	endif	

    SendSysMessage(caster, "Select victim.");
    var victim := Target(caster);
    var amount:= 0;
    var victimID:=GetObjProperty(victim, "master");

    if (victimID == caster.serial)
    amount := cint(victim.hp / 10);
    else
	SendSysMessage(caster, "Invalid Target.");
	return;
    endif

    var CasterSkill := GetEffectiveSkill(caster, SKILLID_MAGERY);
    var range := Cint(CasterSkill / 20);
    var targets := ListMobilesNearLocation(victim.x, victim.y, victim.z, range);
    var diestring := "4d" + Cstr(amount);

    

    foreach mobile in targets
	if ((mobile.serial != caster.serial) and (!mobile.dead) and (CheckLineOfSight(Victim, mobile)))
		PlaySoundEffect(mobile, SFX_208);
		PlayObjectCenteredEffect(mobile, 0x036b0, 7, 0x10);
		var dmg := RandomDiceRoll(diestring);
		ApplyRawDamage(mobile, dmg);
	endif
    endforeach

    PlayLightningBoltEffect(victim);
    ApplyRawDamage(victim, (victim.hp + 3));

endprogram
