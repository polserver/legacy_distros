use uo;
use os;
use util;

include "include/client";
include "castopts";
include "include/attributes";

const WraithGraphic := 0x1a;
const WraithColor := 0x482;
const DrainEffect := 0x374a;
const DrainSound := 0x1fa;
const MorphSound := 0x210;
const FlameStrike := 0x3709;

var spelldelay := 4;

program cast_wraithform(caster)

	var options := GetObjProperty(caster, "NecroOpts");
	if (!options)
		options := {};
	else
		options := splitwords(options);
	endif
	EraseObjProperty(caster, "NecroOpts");

	if (IsNoWords(options) == 0)
		SpeakPowerWords(caster, 72);
	endif

	if (IsNoDelay(options) == 0)
		While (spelldelay > 0)
			PerformAction(caster, ANIM_CAST_DIR);
			spelldelay := spelldelay - 1;
			sleep(1);
		endwhile
		sleepms(500);
	endif

	if (IsNoMana(options) == 0)
		if (!ConsumeMana(caster, 72))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Mana.");
			return;
		endif
	endif	

	if (IsNoRegs(options) == 0)
		if (!ConsumeReagents(caster, 72))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			SendSysMessage(caster, "Insufficient Reagents.");
			return;
		endif
	endif

	if (IsNoSkillCheck(options) == 0)
		if (!CheckSkill(caster, SKILLID_MAGERY, 100, 0))
			PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
			PlaySoundEffect(caster, SFX_5B);
			return;
		endif
	endif	

	var OldCharGraphic := caster.graphic;
	var OldCharColor := caster.color;
	var CasterSkill := GetEffectiveSkill(caster, SKILLID_MAGERY);
	var Duration := Cint(CasterSkill / 5);
	var Range := Cint (CasterSkill / 25);
	var PowerLevel := Cint(CasterSkill /10);
	var DamageDie := "2d" + PowerLevel;

	caster.graphic := WraithGraphic;
	caster.color := WraithColor;
	PlayObjectCenteredEffect(caster, FlameStrike, 1, 0x10);
	PlaySoundEffect(caster, MorphSound);



	set_Critical(1);
	Detach();

	While (Duration > 0)
		var targets := ListMobilesNearLocation(caster.x, caster.y, caster.z, Range);

		foreach mobile in targets
			if ((mobile.serial != caster.serial) and (!mobile.dead))
				var Dmg := RandomDiceRoll(DamageDie);
				PlayObjectCenteredEffect(mobile, DrainEffect, 7, 0x10);
				PlaySoundEffect(mobile, DrainSound);
				ApplyRawDamage(mobile, Dmg);
				caster.mp := (caster.mp + Dmg);

				if (caster.mp > caster.maxmp)
					caster.mp := caster.maxmp;
				endif
			endif
		endforeach

		Duration := Duration - 1;
		Sleep (5);

	endwhile

	caster.graphic := OldCharGraphic;
	caster.color := OldCharColor;
	PlayObjectCenteredEffect(caster, FlameStrike, 1, 0x10);
	PlaySoundEffect(caster, MorphSound);

endprogram
