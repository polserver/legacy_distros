use uo;
use os;
use util;

include "include/client";
include "include/attributes";

program poisondamage (passparms)
  var who := passparms[1];
  var attacker := passparms[2];
  var weapon := passparms[3];
  var attackername, attackerserial;
  var timestamp := GetObjProperty(who,"PoisonStamp");
  var npccfg := ReadConfigFile("::npcdesc");
  SetObjProperty(who, "#PoisonPid", GetPid());
  if(who.acctname)
    SetScriptController(passparms[1]);
  endif
  if((npccfg[who.npctemplate].elemental == "poison") || (!GetStrength(who)) || (who.npctemplate == "bladespirit") || (who.npctemplate == "vortex"))
  //  EraseObjProperty(who, "poison_level");
    return;
  endif
  if(who.cmdlevel > 1)
    EraseObjProperty(who,"poisonloops");
    EraseObjProperty(who,"poison_level");
    EraseObjProperty(who,"PoisonStamp");
    who.SetPoisoned(0);
    return;
  endif
  var pimmune := GetObjProperty(who, "a_equipped");
  if(pimmune == "DAEMON")
    EraseObjProperty(who,"poisonloops");
    EraseObjProperty(who,"poison_level");
    EraseObjProperty(who,"PoisonStamp");
    SendSysMessage(who, "The poison has been repelled by your armor!", 3, 40);
    who.SetPoisoned(0);
    return;
  endif
  if(weapon == "PassiveSource");
    attackername := attacker[1];
    attackerserial := attacker[2];
  else
    attackername := attacker.name;
    attackerserial := attacker.serial;
  endif
  var poison_level := CInt(GetObjProperty(who, "poison_level"));
  if(poison_level)
    if(!who.npctemplate)
      who.setpoisoned(1);
    endif
  else
    return;
  endif
  var loops := CInt(GetObjProperty(who,"poisonloops"));
  if(!loops)
    loops := 120;
  endif
  var msg, bcast, dmg, min, stam, sleeper;
  case(poison_level)
    1: sleeper := 15;   min :=  3;   stam := 0;
    2: sleeper := 12;   min :=  5;   stam := 0;
    3: sleeper := 10;   min :=  7;   stam := Cint(GetMaxStamina(who) / 30);
    4: sleeper :=  5;   min :=  6;   stam := Cint(GetMaxStamina(who) / 20);
    5: sleeper :=  5;   min := 12;   stam := Cint(GetMaxStamina(who) / 10);
  endcase
  if(!sleeper)
    EraseObjProperty(who,"poison_level");
    EraseObjProperty(who,"poisonloops");
    EraseObjProperty(who,"PoisonStamp");
    who.SetPoisoned(0);
    return;
  endif
  while((CInt(GetObjProperty(who,"PoisonStamp")) <= CInt(timestamp)) && (!who.dead))
    poison_level := CInt(GetObjProperty(who, "poison_level"));
    case(poison_level)
      1: dmg   := Cint(GetStrength(who) / 40);
         msg   := "You feel a bit nauseous.";
         bcast := "* - " + who.name + " looks ill. - *";
      2: dmg   := Cint(GetStrength(who) / 32);
         msg   := "You feel disoriented and nauseous.";
         bcast := "* - " + who.name + " looks extremely ill. - *";
      3: dmg   := Cint(GetStrength(who) / 16);
         msg   := "You begin to feel pain throughout your body.";
         bcast := "* - " + who.name + " stumbles around in confusion and pain. - *";
      4: dmg   := Cint(GetStrength(who) / 8);
         msg   := "You are in extreme pain, and require immediate aid!";
         bcast := "* - " + who.name + " begins to spasm uncontrollably - *";
      5: dmg   := Cint(GetStrength(who) / 4);
         msg   := "You are struck with a lethal dose, and require immediate aid!";
         bcast := "* - " + who.name + " begins to spasm in deadly convulsions - *";
    endcase
    PrintTextAbovePrivate(who, msg, who);
    foreach mob in ListMobilesInLineOfSight(who, 8)
      if((mob.ip) and (mob != who))
        PrintTextAbovePrivate(who, bcast, mob);
      endif
    endforeach
    if((!GetObjProperty(who, "poison_level")) || (poison_level < 1) || (loops <= 0))
      SendSysMessage(who, "The poison has run its course");
    EraseObjProperty(who, "poison_level");
    EraseObjProperty(who, "poisonloops");
    EraseObjProperty(who, "PoisonStamp");
      who.SetPoisoned(0);
      break;
    endif
    var lasthitparms := {attackername, attackerserial, "poison"};
    SetObjProperty(who, "LastHit", lasthitparms );
    if(dmg < min)
      dmg := min;
    endif
    ApplyRawDamage(who, dmg);
    if(GetStamina(who) >= stam)
      SetStamina(who, Cint(GetStamina(who) - stam));
    else
      SetStamina(who, 0);
    endif
    RecalcVitals(who);
    sleep(sleeper);
    loops := loops - sleeper;
    SetObjProperty(who,"poisonloops", loops);
  endwhile
  EraseObjProperty(who, "#PoisonPid");
endprogram
