use uo;
use os;

include "include/spellRestrictions";
include "include/attributes";
include "include/armor-powers";

program cast_reactivearmor( parms )
  var circle := 1;
  var delivery := "direct";
  var noto := "helpful";
  var result := maincast(parms, delivery, circle, noto, 7);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  PlaySoundEffect(cast_on, SFX_SPELL_REACTIVE_ARMOR);
  PlayObjectCenteredEffect( cast_on, FX_SPARK_EFFECT, 7, 0x10 );
  if(GetObjProperty(cast_on, "ar"))
	SendSysMessage(caster,"Already under the influence!");
	return;
  endif
  var itemdesc := ReadConfigFile("itemdesc");
  foreach item in ListEquippedItems(cast_on)
    if((itemdesc[item.objtype].blockcircle) && !itemdesc[item.objtype].errortext)
      SendSysMessage(cast_on, "Your armor blocks the ability of this spell!");
      return;
    endif
  endforeach
  var magery := GetEffectiveSkill(caster, SKILLID_MAGERY);
  var armlore := GetEffectiveSkill(caster, SKILLID_ARMSLORE);
  var mod_amount := magery / 10;
  var duration := CInt(RandomInt(10) + (magery / 2));
  mod_amount := mod_amount + (armlore / 10);
  SetObjProperty(cast_on, "ReactiveArmor", mod_amount);
  Detach();
  sleep(duration);
  if(GetObjProperty(cast_on, "ReactiveArmor"))
    PlaySoundEffect(cast_on, 0x1eb);
    EraseObjProperty(cast_on,"ReactiveArmor");
  endif
endprogram
