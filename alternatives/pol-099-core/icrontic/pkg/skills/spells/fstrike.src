use uo;
use cfgfile;

include "include/spellRestrictions";
include "include/attributes";

program cast_flamestrike( parms )
  var circle := 7;
  var delivery := "direct";
  var noto := "harmful";
  var result := maincast(parms, delivery, circle, noto, 51);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  var dmg := RandomDiceRoll("2d10+40");
  SetObjProperty(cast_on, "LastHit", {caster.name,caster.serial, "flame strike" });
  if (Reflected(cast_on))
	cast_on := caster;
  endif
  PlayObjectCenteredEffect(cast_on, 0x3709, 0x0a, 0x1e );
  PlaySoundEffect( caster, SFX_SPELL_FLAME_STRIKE );
  if(GetHp(cast_on) >= 1)
    if(cast_on.npctemplate)
      dmg := dmg * 2;
    endif
    ApplyRawDamage(cast_on, Resisted(circle,caster,cast_on,dmg));
  endif
endprogram
