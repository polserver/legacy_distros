///////////////////
//  This program is called when you double click a beehive.  It produces honey and wax
///////////////////

use uo;
use os;

include "include/yesno";
include "include/client";

Const ITEM_SWARM		:= 0x091b;
Const SOUND_SCRUNCH		:= 0x58;
Const ACTION_BOW		:= 0x0020;


program harvest_honey (character, beehive)

	CreateItemAtLocation (beehive.x, beehive.y, beehive.z, ITEM_SWARM, 1);

	//this is the amount of honey and wax that is harvested on each successful attempt
	var harvest_wax_per_try := 5;
	var harvest_honey_per_try := 5;

	var hive_wax_amount := GetObjProperty(beehive, "Wax");
	var hive_honey_amount := GetObjProperty(beehive, "Honey");

	//if there's no honey or wax, ask the user if they want to destroy the hive
	if (hive_wax_amount < 1 and hive_honey_amount < 1)
		SendSysmessage (character, "The hive is empty.");
		SendSysmessage (character, "Do you want to destroy the beehive?");
		var destroy := YesNo(character,"Destroy?");
		if (destroy)
			CreateItemAtLocation (beehive.x, beehive.y, beehive.z, ITEM_SWARM, 1);
			DestroyItem (beehive);
			SendSysmessage (character, "You destroy the hive.  The bees are very angry.");
			return;
		else
			return;
		endif
	else
		SendSysmessage (character, "You begin harvesting.");
		var characterx := character.x;
		var charactery := character.y;
		repeat

			//action, sound, and time delay
			PlaySoundEffect( character, SOUND_SCRUNCH );
			PerformAction( character, ACTION_BOW );
			sleep (2);
			PlaySoundEffect( character, SOUND_SCRUNCH );
			PerformAction( character, ACTION_BOW );
			sleep (3);

			//do a skill check
			if ( checkskill(character, SKILLID_ANIMALLORE, -1, 0) )

				//if it was successful, give the character some wax, decrease the wax counter set the hive
				if (hive_wax_amount < harvest_wax_per_try)
					CreateItemInBackpack (character, 0x1422, hive_wax_amount);
					hive_wax_amount := 0;
					SetObjProperty (beehive, "Wax", 0);
				else
					CreateItemInBackpack (character, 0x1422, harvest_wax_per_try);
					hive_wax_amount := hive_wax_amount - harvest_wax_per_try;
					SetObjProperty (beehive, "Wax", hive_wax_amount );
				endif

				//do the same for honey
				if (hive_honey_amount < harvest_honey_per_try)
					CreateItemInBackpack (character, 0x09ec, hive_honey_amount);
					hive_honey_amount := 0;
					SetObjProperty (beehive, "Honey", 0);
				else
					CreateItemInBackpack (character, 0x09ec , harvest_honey_per_try);
					hive_honey_amount := hive_honey_amount - harvest_honey_per_try;
					SetObjProperty (beehive, "Honey", hive_honey_amount);
				endif

				//nice message
				SendSysMessage (character, "You place the items in your backpack.");

				//if we're out of both wax and honey, send a mesage and end
				if (hive_wax_amount < 1 and hive_honey_amount < 1)
					SendSysMessage (character, "The hive is empty.  Harvest again to destroy.");
			                var parms := {};
			                parms[1] := beehive;
			                parms[2] := CInt(450);
			                start_script("hiveControl", parms);
					return;
				endif

				//time delay
				PlaySoundEffect( character, SOUND_SCRUNCH );
				PerformAction( character, ACTION_BOW );
				sleep (5);
			else
				SendSysMessage (character, "You fail to get any honey or wax.");

				//stuff
			endif
		until (character.x != characterx or character.y != charactery);
		SendSysMessage (character, "You stop harvesting.");
	endif
endprogram
