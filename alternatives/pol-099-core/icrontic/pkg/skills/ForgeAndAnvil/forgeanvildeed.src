/////////////////////////////////////////////////////////////////////////////
//
// Created by Shurugwi Jan 10, 2001
// - dclick on the deed to create.
// - Once the forge or anvil is created the deed is detroyed.
// - Package is based on ZULU's Trashcan Package. (Thanx ZULU)
// 
/////////////////////////////////////////////////////////////////////////////

use os;
use uo;

const SEARCH_RANGE := 10;
const SMALLFORGE   := 0x0fb1;
const NORTHANVIL   := 0x0fb0;
const WESTANVIL   := 0x0faf;
const CANCEL_ID         := 9999;

program makeforge_anvil( who, deed )
	var deedobj;
	if (deed.objtype = 0x7701)
		deedobj := MakeAndProcessMenu(who,deed);
		if (deedobj = CANCEL_ID)
			SendsysMessage(who,"Cancelled");
			return;
		endif
	else
		deedobj  := SMALLFORGE;
	endif
	var y := who.y, x := who.x;
    var deeddesc := DeedDescription(deed);
    var description := "";
    var exceptional;
    var metalname;
    var cown := 0 ;
    var i;
    var where;
    var place;
    if (!ReserveItem(Deed))
    	SendSysMessage(who,"That's being used by someone else");
    	return;
    endif
    if (!who.multi)
 		SendSysMessage(who, "You may only place this item in a house.",3,31);
    	return;
    endif
	var coownlist := GetObjProperty(who.multi, "Co-Owner");
	for(i := 1; i <= 5; i := i + 1)
		if(GetObjProperty( who.multi , "Co-owner" + i ) = who.serial)
			cown := 1;
			break;
		endif
	endfor

	if (!(GetObjProperty( who.multi , "owneracct" ) = who.acctname) and !cown)
           if(who.cmdlevel !=5 )
		SendSysMessage(who, "Owner acct: " + GetObjProperty(who.multi , "owneracct" ) + " account: " + who.acctname + " cown: " + cown);
		SendSysMessage(who, "This is not your house.",3,31);
	    return;
           endif
	endif
	SendSysMessage( who, "Where would you like to place your " + deeddesc + "?");
    where := TargetCoordinates( who );
    If (!where)
      SendSysMessage( who, "Cancelled");
      return;
    else
    	if(CheckLosAt( who, where.x, where.y, who.z  ))
    		if ((who.x != x) or (who.y != y))
    			SendSysMessage(who, "You can't move while placing a " + deeddesc);
    			return;
    		endif
			place := CreateItemAtLocation( where.x, where.y, who.z, deedobj, 1 );
		else
			SendSysMessage(who, "You cannot see that!");
			return;
		endif
	endif

    If (!place)
    	SendSysMessage( who, "Your " + deeddesc + " has not been created");
    	return;
    endif
	//Make sure the new item has the deed's properties
	if(GetObjProperty(deed, "metalname"))
		if(GetObjProperty(deed, "exceptional"))
			description := "Exceptional "; 
		endif
		if(GetObjProperty(deed, "metalname"))
			metalname := GetObjProperty(deed, "metalname") + " ";
		endif
		description := description + metalname + deeddesc;
		if(GetObjProperty(deed, "exceptional"))
			description := description + " [Crafted by " + GetObjProperty(deed, "MadeBy") + "]"; 
		endif
		place.name := description;
		if(GetObjProperty(deed, "metalname"))
			SetObjProperty(place, "metalname", GetObjProperty(deed, "metalname"));
		endif
		if(GetObjProperty(deed, "exceptional"))
			SetObjProperty(place, "exceptional", GetObjProperty(deed, "exceptional"));
		endif
		if(GetObjProperty(deed, "power"))
			SetObjProperty(place, "power", GetObjProperty(deed, "power"));
		endif
	else
		place.name := deeddesc;
	endif
	place.color := deed.color;
	SetObjProperty(place, "placedby", who.name);
	SetObjProperty(place, "account", who.acctname);
	SetObjProperty(place, "MadeBy",GetObjProperty(deed, "MadeBy"));
	SetObjProperty(place, "ownerserial",GetObjProperty( who.multi , "ownerserial" ));
	place.usescript := ":forge_and_anvil_deeds:redeedforgeanvil";
	SendSysMessage( who, "You have successfully placed the " + deeddesc + ".", 3, 48);
	SendSysMessage( who, "Double click the " + deeddesc + " to get a deed back.", 3, 48);
	DestroyItem(deed);
endprogram

function DeedDescription(deed)

    case(deed.objtype)
		0x7700:	// Small Forge
		    return "Small Forge";
		0x7701:	// North Anvil
		    return "anvil";
    endcase


endfunction

function DeedType(deed)

    case(deed.objtype)
		0x7700:	// Small Forge
		    return SMALLFORGE;
		0x7701:	// North Anvil
		    return 3;
    endcase


endfunction

function MakeAndProcessMenu(who, deed)
  var menu := CreateMenu("Choose an orientation");
  var label := "[North-South]";
  var label2 := "[West-East]";
  AddMenuItem(menu, 0x0faf, label);
  AddMenuItem(menu, 0x0fb0, label2);
  var choice := SelectMenuItem2(who, menu).index;
  if(!choice)
    return CANCEL_ID;
  endif
  Case (choice)
  	1: return 0x0faf;
  	2: return 0x0fb0;
  	default: return CANCEL_ID;
  endcase
endfunction
