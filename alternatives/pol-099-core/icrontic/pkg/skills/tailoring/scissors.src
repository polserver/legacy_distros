use uo;
use cfgfile;

include "include/objtype";
include "include/attributes";
include "include/client";
include "include/canAccess";
include "include/toolWear";

const UOBJ_BOLT_START   := 0x0f95;
const UOBJ_BOLT_END     := 0x0f9c;
const UOBJ_CLOTH_STARTS := 0x175d;
const UOBJ_CLOTH_END    := 0x1768;
const UOBJ_MUTLI_CLOTH  := 0x1766;

var scissors_cfg := ReadConfigFile("scissors");
var cfg2 := ReadConfigFile("tailoring");

program use_scissors( who, scissors )
  EraseObjProperty(who, "IsMeditating");
  if(!can_access(who, scissors))
    return;
  endif
  if(scissors.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  SendSysMessage(who, "Select an item to cut.");
  var cloth := Target(who);
  if(!cloth)
    SendSysMessage(who, "Cancelled.");
    return;
  endif
  if(!Accessible(who, cloth))
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  if(!can_access(who, cloth))
    return;
  endif
  if(cloth.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  //Devious) Guildclothes
  var guildabv:= Splitwords(cloth.name);
  guildabv:= guildabv[len(guildabv)];
  var abv;
  if((guildabv[1] = "[") and (guildabv[len(guildabv)] = "]") and (len(guildabv) < 7))
    SendSysMessage(who, "You cannot use guild clothing.");
    return;
  endif
  // End Devious Guild Package
  var material := Cint(scissors_cfg[cloth.objtype].material);
  var clr := cloth.color;
  var cont := cloth.container;
  var amt, newcloth;
  var obj_type := cloth.objtype;
  if(material)
    amt := (material * cloth.amount);
    if(SubtractAmount(cloth, amt))
     if((obj_type >= UOBJ_BOLT_START)  && (obj_type <= UOBJ_BOLT_END))
      newcloth := CreateItemAtLocation(who.x, who.y, who.z, 0x1766, amt);
      newcloth.color := clr;
      if (cont)
        MoveItemToContainer(newcloth, cont);
      else
        MoveItemToContainer(newcloth, who.backpack);
      endif
	  CheckToolWear (who, scissors, SKILLID_TAILORING);
      PlaySoundEffect(who, 0x0249);
      SendSysMessage(who, "You cut the bolts into cloth and put them in your pack.");
     else
      newcloth := CreateItemAtLocation(who.x, who.y, who.z, 0xe21, amt);
      newcloth.color := clr;
      if (cont)
        MoveItemToContainer(newcloth, cont);
      else
        MoveItemToContainer(newcloth, who.backpack);
      endif
	  CheckToolWear (who, scissors, SKILLID_TAILORING);
      PlaySoundEffect(who, 0x0249);
      if(amt > 1)
        SendSysMessage(who, "You create some bandages and put them in your pack.");
      else
        SendSysMessage(who, "You create a bandage and put it in your pack.");
      endif
     endif
      return;
    else
      SendSysMessage(who, "You can not cut that.");
      return;
    endif
  else
    material := Cint(cfg2[cloth.objtype].material);
    if(material)
      PlaySoundEffect(who, 0x0249);
	  amt := CInt((material * GetEffectiveSkill(who,SKILLID_TAILORING) * 75) / 10000);
	  if(amt < 1)
        amt := 1;
	  endif
      if(DestroyItem(cloth))
        newcloth := CreateItemInContainer(who.backpack, 0x1766, amt);
        CheckToolWear (who, scissors, SKILLID_TAILORING);
        if(newcloth)
          newcloth.color := clr;
        endif
      else
        SendSysMessage(who, "you cannot cut that into cloth.");
        return;
      endif
    else
      SendSysMessage(who, "you cannot cut that into cloth.");
      return;
    endif
  endif
endprogram
