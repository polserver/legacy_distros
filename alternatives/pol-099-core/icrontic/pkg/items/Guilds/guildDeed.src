											/*
								         guildDeed.src	//
								        Package guilds	//
								   To create the guild	//
											//
//////////////////////////////////////////////////////////////////////////////////////////

                      ////    ////// //   //  //  //////  //  //  //////
                     //  //  //      //  //  //  //  //  //  //  //
                    //  //  ////     // //  //  //  //  //  //  //////
                   //  //  //        ////  //  //  //  //  //      //
                  /////   //////     ///  //  //////  //////  //////

//////////////////////////////////////////////////////////////////////////////////////////
//
//  This file was created by Developer Devious of Neverlands Shard
//  http://www.neverlands.org/
//  devtempo@telusplanet.net
//  Released under extraordinary circumstances, never trust Ego-Crusher, Icrontic, Kain,
//    "Shai`tain", ever, for he was the reason these files were released, beyond my will.
//  I hope everybody enjoys these scripts, and give credit where it is due, unlike the
//    backstabbing bastard above who claimed my scripts and nearly released them before
//    I did.
*/





use uo;
use os;

include "include/canAccess";
include "guild";

const UOBJ_GUILDSTONE:= 0xa392;
const MAX_GUILD_NAME_SIZE:= 30;
const MAX_GUILD_ABREV_SIZE:= 4;
const UOBJECT_DOORS_START:= 0x0675;
const UOBJECT_DOORS_END:= 0x06f4;

const COLOUR_START:= 2432;	//For guild clothes, where do guild colours start?

program use_guild_deed(who,deed)

  EraseObjProperty(who,"IsMeditating");
  EraseObjProperty(who,"HealTimer");

  if(!can_access(who, deed))
    return;
  elseif(!ReserveItem(deed))
    return;
  endif

  if(GetObjProperty(who,"guild_id"))
    SendSysMessage(who,"You are already a member of a Guild.");
    return;
  endif

  if(not Accessible(who,deed))
    SendSysMessage(who,"You cannot access that item!");
    return;
  endif

  var check:= 0;
  var owner:= GetObjProperty(who.multi,"owneracct");
  var coowners:= GetObjProperty(who.multi,"coownlist");

  if(owner = who.acctname)
    check:= 1;
  elseif(who.acctname in coowners)
    check:= 1;
  endif

  if(check = 0)
    SendSysMessage(who,"You must be in a house you own to place this.");
    return;
  endif

  foreach item in ListItemsNearLocation(who.x,who.y,who.z,1)
    if((item.objtype >= UOBJECT_DOORS_START) and (item.objtype <= UOBJECT_DOORS_END))
      if((((item.x = who.x + 1) or (item.x = who.x - 1)) and (item.y = who.y)) or (((item.y = who.y + 1) or (item.y = who.y - 1)) and (item.x = who.x)))
        SendSysMessage(who,"This is too close to the door.");
        return;
      endif
    endif
  endforeach

  var hs:= who.multi;

  foreach thing in (hs.components)
    if((thing.objtype = 0xbd2) or (thing.objtype = 0xbd0))
      hs:= thing.serial;
      break;
    endif
  endforeach

  who.frozen:= 1;
  var item:= CreateItemAtLocation(0,0,0,UOBJ_GUILDSTONE,1);
  var setup:= setupguild(who,item);
  var pos:= GetStandingHeight(who.x,who.y,who.z);
  item.movable:= 1;
  MoveItemToLocation(item,who.x,who.y,pos.z,0x40000000);
  item.movable:= 0;
  who.frozen:= 0;

  if(!setup)
    DestroyItem(item);
  else
    DestroyItem(deed);
    return;
  endif

  SetObjProperty(item,"houseserial",hs);

endprogram


function setupguild(who,stone)

  var tname:= RequestInput(who,who.backpack,"Insert the new guild name.");

  if(!tname)
    return;
  endif

  if(len(tname) > MAX_GUILD_NAME_SIZE)
    SendSysMessage(who,"Guild name can't have more than "+ MAX_GUILD_NAME_SIZE +" characters.");
    return;
  endif

  var allguilds:= ListGuilds();
  var dupe:= 0;
  var i;

  for(i:= 1; i <= allguilds.size(); i:= i + 1)
    if(lower(allguilds[i].getprop("guildname")) = lower(tname))
      dupe:= 1;
    endif
  endfor

  if(dupe = 1)
    SendSysMessage(stone,"That guild name is already taken.");
    return;
  endif

  if(!NameValidation(tname))
    SendSysMessage(who,"You are using invalid characters or names.");
    return;
  endif

  var aname:= RequestInput(who,who.backpack,"Insert an abbreviation.");

  if(!aname)
    return;
  endif

  if(len(aname) > MAX_GUILD_ABREV_SIZE)
    SendSysMessage(who,"Abbreviation can't have more than "+ MAX_GUILD_ABREV_SIZE +" characters.");
    return;
  endif

  dupe:= 0;

  for(i:= 1; i <= allguilds.size(); i:= i + 1)
    if(lower(allguilds[i].getprop("guildabv")) = lower(aname))
      dupe:= 1;
    endif
  endfor

  if(dupe = 1)
    SendSysMessage(stone,"That abbreviation is already in use.");
    return;
  endif

  if(!NameValidation(aname))
    SendSysMessage(who,"You are using invalid characters or names.");
    return;
  endif

  var oriname:= who.name;

  var guild:= CreateGuild();
  var guild_id:= guild.guildid;
  guild.setprop("guildname",tname);
  guild.setprop("guildicon",5545);
  guild.setprop("gm",who.serial);
  dupe:= {};
  dupe[1]:= " ";
  dupe[2]:= " ";
  dupe[3]:= " ";
  dupe[4]:= " ";
  dupe[5]:= " ";
  dupe[6]:= " ";
  dupe[7]:= " ";
  dupe[8]:= " ";
  dupe[9]:= " ";
  guild.setprop("gc",dupe);
  dupe:= {};
  guild.setprop("recruits",dupe);
  guild.setprop("movetime",ReadGameClock()+(7*24*3600));
  guild.setprop("nametime",ReadGameClock()+(7*24*3600));
  guild.setprop("abrvtime",ReadGameClock()+(7*24*3600));
  guild.setprop("typetime",ReadGameClock());
  guild.setprop("faction","Newcomer");
  guild.setprop("type","Standard");
  guild.setprop("guildabv",aname);
  guild.setprop("stone",stone.serial);
  guild.setprop("website","http://www.icrontic.net");
  stone.name:= "Guildstone for "+ tname;
  stone.color:= COLOUR_START;
  SetObjProperty(stone,"guild_id",guild_id);
  SetObjProperty(who,"guild_id",guild_id);
  SetObjProperty(who,"guild_fealty",who.serial);
  var clothing:= CreateItemInBackpack(who,0x1541);
  clothing.color:= COLOUR_START;
  var guildname;
  if(guild.getprop("guildabv") = "NONE")
    guildname:= guild.getprop("guildname");
  else
    guildname:= guild.getprop("guildname") +" ["+ guild.getprop("guildabv") +"]";
  endif
  clothing.name:= guildname;
  clothing.newbie:= 1;
  SetObjProperty(who,"guild_clothing",clothing.serial);
  SetObjProperty(who,"guild_title","Guildmaster");
  who.title_guild:= "Guildmaster, ["+ aname +"]";
  who.name:= oriname;
  var test:= guild.addmember(who);
  return 1;

endfunction


function NameValidation(thestring)

  var validstr:= {"0","1","2","3","4","5","6","7","8","9","a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z","$","*","&","#","!","+","-"," ","'"};
  var invalidnames:= {"fuck","shit","cunt","bitch","whore","slut","motherfucker","stfu","69","dick","penis","asshole","pussy","staff","gm","gamemaster","seer","counselor","councilor","councillor","admin","administrator","trainee","developer","oracle","soothsayer","inv","invulnerable","frozen","suxx","sucks","suxx0rs","sux0rs", "lol", "slut"};
  var i;

  thestring:= lower(thestring);

  for(i:= 1; i <= len(thestring); i:= i + 1)
    if(!(thestring[i] in validstr))
      return 0;
    endif
  endfor

  thestring:= SplitWords(thestring);

  for(i:= 1; i <= len(thestring); i:= i + 1)
    if(thestring[i] in invalidnames)
      return 0;
    endif
  endfor

  return 1;
  
endfunction
