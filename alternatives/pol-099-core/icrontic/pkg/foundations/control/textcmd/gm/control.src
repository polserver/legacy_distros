//--> control <--
//
// CONTROL:TEXTCMD/GM/CONTROL.SRC
//
// Accesses the list of registered packages and brings
// up a gump to access them from :)
//
// Creator: TJ Houston (Myrathi)
// Contact: myrathi@templar.dhs.org
//
// Created: 05-Apr-2000
// Updated: 01-May-2000
//
use uo;
use os;
use basic;

include "../../../../foundations/gumps/gumps";
//include ":gumps:gumps"; // not supported until a future compiler release. BLAH! :P

// Gump constants
//
// (also see top of BuildGump() function)
//
const WINX_MAX := 640;
const WINY_MAX := 480;

// Control.src constants
//
const CTRL_VER := "0.5a";
const CTRL_INST := "#CTRL_Installed";

enum CTRL
	CTRL_NAME := 1,	//1
	CTRL_VERSION,	//2
	CTRL_HASIFACE,	//3
	CTRL_SCRIPT		//4
endenum

// Global list of gump-button IDs
//
var pkg_ids;
var pkg_scripts;

program textcmd_control( who, params )

	if ( params["-r"] or params["refresh"] )
		SendSysMessage( who, "Refreshing registered package list..." );
		if ( run_script_to_completion( ":control:check_packages" ) )
			SendSysMessage( who, "Refresh complete!" );
		else
			SendSysMessage( who, "Refresh failed!" );
			return;
		endif
	endif

	var zList := GetGlobalProperty( CTRL_INST );
	if ( !zList )
		SendSysMessage( who, "Controller Package has not initialised yet." );
		return;
	endif

	if ( zList = -1 )
		SendSysMessage( who, "There are no registered packages." );
		return;
	endif

	var exit_id := BuildGump( zList );
	if ( !exit_id )
		SendSysMessage( who, "Failed to build the ICP gump." );
		return;
	endif

	var retv := GFSendGump( who );
	if ( !retv and (retv <> 0) )
		SendSysMessage( who, "Please close any other open gumps and try again." );
		return;
	endif

	if ( retv[0] in pkg_ids )
		var idx := 1;
		while( (pkg_ids[idx] <> retv[0]) and (idx <= pkg_ids.size()) )
			idx := idx + 1;
		endwhile
		if ( retv[0] <> pkg_ids[idx] )
			SendSysMessage( who, "Corrupted PackageID array!" );
			print( "[CONTROL] Corrupted PackageID array! Argh!" );
			return;
		endif
		run_script_to_completion( pkg_scripts[idx], { "ICP", who } );
	else
		SendSysMessage( who, "Retv[0]/cancel = " + Hex(retv[0]) + "/" + Hex(exit_id) );
	endif

	return;

endprogram

////////////////////////////////////////////////////////////
const HEAD_COL := 56; // 98
//
const PANEX_LEFT := 20;
const PANEX_RIGHT := WINX_MAX - PANEX_LEFT;
const PANEW_ALL := PANEX_RIGHT - PANEX_LEFT;
//
const PANEY_TOP := 0; //30
const PANEH_TOP := 41;
//
const PANEY_MID := PANEY_TOP + 50;
const PANEH_MID := WINY_MAX - PANEY_MID - 30;
//
const PANEX_LIST := 30;
const PANEY_LIST := 10;
const PANEW_LIST := (PANEW_ALL - (2 * PANEX_LIST));
const PANEH_LIST := PANEH_MID - 30;
//
const PANEW_VER := 110;
////////////////////////////////////////////////////////////

// Builds the basic gump structure, excluding package info
//
// Returns: The 'keyid' for the gump-exit button
//
function BuildGump( zList )

	var exit_but;

		pkg_ids := { };
		pkg_scripts := { };

		GFInitGump();
		GFNoClose();
		GFNoMove();

		// Page 0

			GFResizePic(0, 0, BKGND_GOLDTRIM, WINX_MAX+3, WINY_MAX+4);
			GFTextMid(1, 12, WINX_MAX, 55, "Interface Controller Package" );
			GFTextLine(6, WINY_MAX - 21, 32, "v" + CTRL_VER );
exit_but :=	GFButtonID(WINX_MAX - 65, 11, 0x849, 0x848, 1); // Cancel  (0x81C, 0x81B = OKAY)

			GFSetRef(PANEX_LEFT, PANEY_MID);
			//GFResizePic(0, 0, BKGND_STONESLAB, PANEW_ALL, PANEH_MID);

			GFSetRefRel(PANEX_LIST, PANEY_LIST);
			AddScrollAndHeader();

		// Page 1 - ?
			var pkg_per_page := 16;
			var nMaxPage := CInt( zList.size() / pkg_per_page ) + 1;
			if ( Mod(zList.size(), pkg_per_page) = 0 )
				nMaxPage := nMaxPage - 1;
			endif

			for nPage := 1 to nMaxPage
				GFPage( nPage );
				AddPackageEntries( zlist, ((nPage-1)*pkg_per_page)+1, nPage * pkg_per_page );

				AddPageButtons( nPage, nMaxPage );
			endfor

	return (exit_but.keyid);

endfunction

//------------------------------------------------------------

function AddScrollAndHeader()

	GFResizePic(0, 0, BKGND_SCROLL, PANEW_LIST, PANEH_LIST);
	GFGumpPic(35, 10, 0xFD); // down arrow within gold raise
	GFTextLine(56, 10, HEAD_COL, "Package Name" );
	GFTextMid(PANEW_LIST - PANEW_VER - 32, 10, PANEW_VER, HEAD_COL, "Version" );

	return;

endfunction

// Gem buttons for gump
const GEM_U := 0x4BA; // release
const GEM_D := 0x4B9; // press
const PKG_COL := 47;

function AddPackageEntries( zPkgs, lBound, uBound )

	// If no array passed, or if it's empty, return immediately
	//
	if ( !zPkgs )
		syslog( "Bad PKGLIST" );
		return;
	elseif( zPkgs.size() = 0 )
		syslog( "Empty PKGLIST" );
		return;
	endif

	// Validate the lower array-bounds
	if ( lBound > zPkgs.size() )
		return;
	endif

	// Validate the upper array-bounds
	if ( uBound < lBound )
		uBound := lBound;
	elseif ( uBound > zPkgs.size() )
		uBound := zPkgs.size();
	endif

	// Change the relative origin and store the old absolute [x,y]
	var oldref := GFSetRefRel( 35, 37 );

	// Initialise offset and delta-y
	var offy := 0, dy := 18;

	// Display each package
	//
	for i := lBound to uBound
		var zItem := zPkgs[i];

		var sName := zItem[CTRL_NAME];
		var sVer := zItem[CTRL_VERSION];
		var bCtrl := zItem[CTRL_HASIFACE];
		var sScript := zItem[CTRL_SCRIPT];

		GFTextCrop( 21, offy, PANEW_LIST-172, dy, PKG_COL, sName );
		GFTextMid( PANEW_LIST-PANEW_VER-(35+32), offy, PANEW_VER, PKG_COL, sVer );

		if ( bCtrl )
			var button := GFButtonID( 0, offy+3, GEM_U, GEM_D, 1 );
			pkg_ids.append( button.keyid );
			pkg_scripts.append( sScript );
		endif

		offy := offy + dy;
		sleepms(2);
	endfor

	// Reset the origin
	GFSetRef( oldref.x, oldref.y );

	return;

endfunction

//------------------------------------------------------------

// Gump prev/next button IDs
const GEM_PREV := 0x8AF;
const GEM_NEXT := 0x8B0;

const GEM_LEFT := 33;
const GEM_RIGHT := (-43);
const GEM_UP := 27;

function AddPageButtons( nPage, nMaxPage )

	if ( nPage > 1 )
		GFButtonPage( GEM_LEFT, PANEH_LIST - GEM_UP, GEM_PREV, GEM_PREV, nPage-1 );
		GFTextLine( GEM_LEFT + 24, PANEH_LIST - GEM_UP - 4, HEAD_COL, "Prev" );
	endif

	if ( nPage < nMaxPage )
		GFButtonPage( PANEW_LIST + GEM_RIGHT, PANEH_LIST - GEM_UP, GEM_NEXT, GEM_NEXT, nPage+1 );
		GFTextRight( PANEW_LIST + GEM_RIGHT - 12, PANEH_LIST - GEM_UP - 4, HEAD_COL, "Next" );
	endif

endfunction

//------------------------------------------------------------

// A raped version of Sigismund's "Mod()" function. Credits to him! :)
// Put it here so I didn't have to rely on "math.inc"
function Mod(num, denom)
	var dvd := num-(CInt(num/denom)*denom);
	return dvd;
endfunction
