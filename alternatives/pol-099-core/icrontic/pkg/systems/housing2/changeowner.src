////////////
//
// changeowner.ecl gets call when a player dbl clicks on the house title
// that was created in his bank. It changes the owner props on the house it
// belongs to. Use that title to change the ownership of the house.
// KEEP IT SAFE!
//
// Author: Racalac, 9/12/99
// Maintainer: Bishop Ebonhand
////////////

use uo;
use os;

include "util/bank";
include "util/key";
Const UOBJECT_DOORS_START := 0x0675;
Const UOBJECT_DOORS_END   := 0x06f4;
Const UOBJECT_BRASS_SIGN := 0x0bd2;
Const UOBJECT_WOOD_SIGN := 0x0bd0;

program ChangeHouseOwner( who , title )
  var houseserial := GetObjProperty( title, "builtserial" );
  if( houseserial = error )
    SendSysmessage( who, "That title is invalid" );
    return;
  endif
  var house :=  SystemFindObjectBySerial( houseserial );
  if( house = error )
    SendSysmessage( who , "Error finding the built house!" );
    syslog( house.errortext );
    return;
  endif
//  var oldownerserial := GetObjProperty( house , "ownerserial" );
//  if( oldownerserial = who.serial )
//    SendSysmessage( who , "You are already the owner of that house." );
//    return;
//  else
    release(house);
    var bankbox := FindBankBox( who );
    var packkey := CreateItemInBackpack( who, UOBJ_GOLD_KEY );
    if (!packkey)
        sendsysmessage( who, "My backpack is too full!");
        return;
    endif
    var bankkey := CreateItemInContainer( bankbox, UOBJ_GOLD_KEY );
    if (!bankkey)
        sendsysmessage( who, "My bankbox is too full!");
        DestroyItem( packkey );
        return;
    endif
    var lockid := AllocLockId();
    SetObjProperty( packkey, "lockid", lockid );
    SetObjProperty( bankkey, "lockid", lockid );
    var item := house.components;
    foreach itemt in item
      if( (itemt.objtype = UOBJECT_BRASS_SIGN) or (itemt.objtype = UOBJECT_WOOD_SIGN) )
        SetObjProperty(itemt, "lockid", lockid);
        SetObjProperty( itemt , "numlockdowns" , Cint(GetObjProperty( title, "numlockdowns" )));
        SetObjProperty( itemt , "numsecure" , Cint(GetObjProperty( title, "numsecure" )));
        SetObjProperty( itemt , "ownerserial" , who.serial );  
        SetObjProperty( itemt , "owneracct" , who.acctname );
      elseif( ((itemt.objtype >= UOBJECT_DOORS_START) and (itemt.objtype <= UOBJECT_DOORS_END)))
        itemt.locked := 1;
        SetObjProperty(itemt, "lockid", lockid);
      endif  
    endforeach
    MoveItemToContainer( title, bankbox, x := -1, y := -1 );
    SendSysmessage( who , "You have taken ownership of the house." );
//  endif
endprogram

function release(house)
  var item := house.items;
  foreach itemt in item
    if ((getobjproperty(itemt,"secure") = 1) and (itemt.movable = 0))
      eraseobjproperty(itemt,"secure");
      var oldscript := getobjproperty(itemt,"oldscript");
      if (oldscript = error)
        oldscript := "";
      endif
      itemt.usescript := oldscript;
      itemt.movable := 1;
      EraseObjProperty( itemt , "houseserial" );
      EraseObjProperty( itemt , "oldscript" );
      var newbox := SystemFindObjectBySerial( GetObjProperty( itemt, "boxserial") );
      var items := enumerateitemsincontainer(newbox);
      foreach item in items
        moveitemtocontainer(item, itemt);
      endforeach
      destroyitem(newbox);
      EraseObjProperty( itemt, "boxserial");
    elseif ((GetObjProperty( itemt , "lockeddown" ) = 1) and (itemt.movable = 0))
      itemt.movable := 1;
      eraseobjproperty(itemt, "lockeddown");
    endif
  endforeach
  return;
endfunction
   
