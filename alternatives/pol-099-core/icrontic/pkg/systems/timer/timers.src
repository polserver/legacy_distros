use uo;
use os;

const LONG_COUNT_DECAY_TIME := 72000; //20 hours
const SHORT_COUNT_DECAY_TIME := 14400; //4 hours

program timers()
  while(1)
    foreach character in EnumerateOnlineCharacters()
      sleepms(1);
      var sessiontime := ReadGameClock() - GetObjProperty(character,"logontime");
      SetObjProperty(character, "logontime", ReadGameClock());
      var timer := CInt(GetObjProperty(character, "onlinetimer"));
      SetObjProperty(character,"onlinetimer", Cint(timer) + Cint(sessiontime));
      CheckSkillbonus(character);
      CheckMurderCountDecay(character);
      CheckFameDecay(character);
    endforeach
    sleep(300);
  endwhile
endprogram

function CheckMurderCountDecay(character);
  var long_term_counts := CInt(GetObjProperty(character,"longmurders"));
  if(long_term_counts > 0)
    CheckLongTermCounts(character);
  endif
  var short_term_counts := CInt(GetObjProperty(character,"shortmurders"));
  if(short_term_counts > 0)
    CheckShortTermCounts(character);
  endif
endfunction

function CheckLongTermCounts(character);
  var longdecaytime := CInt(GetObjProperty(character, "decaylongcountat"));
  var onlinetime := GetObjProperty(character, "onlinetimer");
  if(!longdecaytime)
    longdecaytime := onlinetime + LONG_COUNT_DECAY_TIME;
    SetObjProperty(character, "decaylongcountat", longdecaytime);
    return;
  endif
  if(onlinetime > longdecaytime)
    var longcount := GetObjProperty(character, "longmurders");
    longcount := longcount - 1;
    SetObjProperty(character, "longmurders", longcount);
    if(longcount > 0)
      SetObjProperty(character, "decaylongcountat", (onlinetime + LONG_COUNT_DECAY_TIME));
      if((longcount < 5) && (!GetObjProperty(character, "permared")) )
        character.setMurderer(0);
      endif
    endif
  endif
endfunction

function CheckShortTermCounts(character);
  var shortdecaytime := GetObjProperty(character, "decayshortcountat");
  var onlinetime := GetObjProperty(character, "onlinetimer");
  if(!shortdecaytime)
    shortdecaytime := onlinetime + SHORT_COUNT_DECAY_TIME;
    SetObjProperty(character, "decayshortcountat", shortdecaytime);
    return;
  endif
  if(onlinetime > shortdecaytime)
    var shortcount := GetObjProperty(character, "shortmurders");
    shortcount := shortcount - 1;
    SetObjProperty(character, "shortmurders", shortcount);
    if(shortcount == 4)
      var pingpong := CInt(GetObjProperty(character, "pingpong"));
      pingpong := pingpong + 1;
      SetObjProperty(character, "pingpong", pingpong);
      if(pingpong >= 5)
        SetObjProperty(character, "permared", 1);
      endif
    endif
    if(shortcount > 0)
        SetObjProperty(character, "decayshortcountat", onlinetime + SHORT_COUNT_DECAY_TIME);
    endif
  endif
endfunction

function CheckFameDecay(who)
	var NextDecay := GetObjProperty(who,"NextFameDecay");
	if (NextDecay >= ReadGameClock())
		var Fame := CInt(GetObjProperty(who,"Fame"));
		if ((!Fame) or (Fame < 1))
			return;
		endif
		var FameMod := CInt(Fame / 100);
		if (FameMod < 1)
			FameMod := 1;
		endif
		SetObjProperty(who,"Fame",CInt(Fame - FameMod));
		SetObjProperty(who,"NextFameDecay",(ReadGameClock() + 7200)); //Next time in 2 hours
	endif
endfunction

function CheckSkillbonus(character)

var TotalTime := GetObjProperty( character, "onlinetimer");
var skillbonus;

if (TotalTime = error)
TotalTime := 0;
endif

// edited my Gex (09/21/02)
if (TotalTime < 144000)
skillbonus:=0;
elseif (TotalTime < 600000) //10k mins 
skillbonus:=100;
elseif (TotalTime < 1800000) // 30k mins
skillbonus:=200;
elseif (TotalTime < 3600000) // 60k mins
skillbonus:=300;
elseif (TotalTime < 6000000) //100k mins
skillbonus:=400;
else
skillbonus:=500;
endif

SetObjProperty(character,"SkillBonus",skillbonus);

endfunction
