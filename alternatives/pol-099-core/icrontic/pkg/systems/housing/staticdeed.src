/////////////////////////////////////////////////
// Static Housing Package
// Version 1.0
// Maintainer: Bishop Ebonhand
// email: bish0p@cfl.rr.com
/////////////////////////////////////////////////
use uo;
use os;
use util;
use basicio;
use basic;
use cfgfile;

include "util/key";
include "include/objtype";
Const UOBJECT_BRASS_SIGN := 0x0bd2;
Const UOBJECT_WOOD_SIGN := 0x0bd0;
Const UOBJECT_TENT_CHEST := 0x0E43;
var cfg;
var colors;
var num_colors;

program staticdeed( character)
        Buildhouse( character);
endprogram

function Buildhouse( character)
    var sign;
	SendSysMessage(character, "Create it where?");
	var where := TargetCoordinates(character);
	if (!where)
		return;
	endif
    var createsign  := makesign(character, where.x, where.y, where.z);
	if (!createsign)
		SendSysMessage( character, "ERROR: Unable to create sign!" );
		return;
	endif
    set_critical( 1 );
    var created := CreateBuiltDeed(character, where.x, where.y, where.z, createsign);
    set_critical( 0 );
    if (!created) 
	SendSysMessage( character, "ERROR: Unable to create built deed!" );
      return;
    endif
endfunction

function CreateBuiltDeed( character, x, y, z, sign)
    var builtdeed := CreateItemInBackpack( character, 0x8888 );
    if (!builtdeed)
      PrintTextAbovePrivate( character, "My backpack is too full!", character );
      return 0; // not built
    endif
    var footprint := boundaries(character, builtdeed, sign);
    SetName(builtdeed, "a static house at " + x + ", " + y + ", " + z + "." );
    return 1; // built ok!
endfunction

function makesign( character, x, y, z)
	var boxmenu := CreateMenu("Select a sign type:");
	AddMenuItem(boxmenu,0xbd1,"brass");
	AddMenuItem(boxmenu,0xbd2,"brass2");
	AddMenuItem(boxmenu,0xbcf,"wooden");
	AddMenuItem(boxmenu,0xbd0,"wooden2");
	var selection := SelectMenuItem2 (character, boxmenu);
    var postgraphic;
    var signgraphic;
    if (selection.objtype = 0xbd1)
      postgraphic := 0xb99;
      signgraphic := 0xbd1;
    elseif (selection.objtype = 0xbd2)
      postgraphic := 0xb98;
      signgraphic := 0xbd2;
    elseif (selection.objtype = 0xbcf)
      postgraphic := 0xb99;
      signgraphic := 0xbcf;
    elseif (selection.objtype = 0xbd0)
      postgraphic := 0xb98;
      signgraphic := 0xbd0;
    endif
	if (!selection)
	  return;
	endif
	var sign := CreateItemAtLocation( x , y , z , 0x7060 , 1 );
	if (!sign)
	  return;
	endif
	sign.graphic := signgraphic;
	sign.movable := 0;
	var post := CreateItemAtLocation( x , y , z , 0x0b98 , 1 );
	if (!post)
	  return;
	endif
	post.movable := 0;
    post.graphic := postgraphic;
    return sign;
endfunction

function boundaries(character, builtdeed, sign)
	var homeinfo := { }; 
	var coords := 0;
	while ( !coords )
	  sendsysmessage(character,"Target the North-West corner of the house (Up!)");
	  coords := TargetCoordinates(character);
	endwhile
	homeinfo[1] := coords.x;
	homeinfo[2] := coords.y;
    homeinfo[6] := coords.z;
	coords := 0;
	while( !coords )
	  sendsysmessage(character,"Target the South-East corner of the house (Down!)");
      coords := TargetCoordinates(character);
	endwhile
	homeinfo[3] := coords.x;
	homeinfo[4] := coords.y;
	homeinfo[5] := sign.serial;
    homeinfo[7] := coords.z;
    var numlockdowns := figurelockdowns( homeinfo[1], homeinfo[2], homeinfo[6], homeinfo[3], homeinfo[4], homeinfo[7]);
    var numsecures   := figuresecures  ( homeinfo[1], homeinfo[2], homeinfo[6], homeinfo[3], homeinfo[4], homeinfo[7]);
    homeinfo[8] := numlockdowns;
    homeinfo[9] := numsecures;
	setobjproperty(builtdeed,"homeinfo",homeinfo);
endfunction

function figurelockdowns( nwx, nwy, nwz, sex, sey, sez)
    var x := (sex-nwx);
    var y := (sey-nwy);
    var z := ((nwz-sez)/20);
    var a := (x*y*z);
    var l := (a/3);
    if (l < 25)
      l := 25;
    endif
    return l;
endfunction

function figuresecures( nwx, nwy, nwz, sex, sey, sez)
    var x := (sex-nwx);
    var y := (sey-nwy);
    var z := ((nwz-sez)/20);
    var a := (x*y*z);
    var s := (a/50);
    if (s < 1)
      s := 1;
    endif
    return s;
endfunction
