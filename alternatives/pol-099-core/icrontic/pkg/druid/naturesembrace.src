use uo;
use os;
use util;

include "include/client";
include "include/eventid";
include "include/spellRestrictions";

program cast_heal( parms )

	var circle := 4;
	var caster;
	var cast_on;

	if (parms[1] = "#MOB")
		caster := parms[2];
	else
		caster := parms;
	endif

	var healed := RandomDiceRoll("3d6");
	var druidic_skills := cint(GetAttribute(caster, SKILLID_MAGERY)) + cint(GetAttribute(caster, SKILLID_ANIMALLORE));
	healed := healed + cint( druidic_skills/10);

	foreach mobile in ListMobilesNearLocation( caster.x, caster.y, caster.z, 10 )
		HealDamage (mobile, healed);
		PlayObjectCenteredEffect (mobile, FX_SPARK_EFFECT, 7, 16);
		PissOff (mobile, caster);
	endforeach

	PlaySoundEffect( caster , SFX_SPELL_HEAL );

	SendSysMessage( caster, "You healed " + healed + " damage." );


endprogram

function PissOff(who,me)

	if (me.npctemplate="humuc")
		return;
	endif

	var ev := {};
        ev.+source := me;
        ev.+type := EVID_DAMAGED;
	foreach mob in ListHostiles(who,9)
		if ( randomint(4) = 1 )
			sendevent(mob, ev);
		endif
	endforeach

endfunction
