use uo;
use os;

include "include/client";
include "include/eventid";
include "include/spellRestrictions";
include "include/castopts";

include "druid";

program groots (parms)

	// some variables
	var circle := 6;
	var caster;
	var cast_on;

	// split the parms. first if the spell was cast by a mobile
	if (parms[1] = "#MOB")
		caster := parms[2];
		cast_on := parms[3];

	//otherwise if it was a player that cast the spell
	else
		caster := parms;
		//make sure they can cast the spell based on their armor
		if (!Can_Cast (caster, circle) )
			EraseObjProperty (caster, "#castingdruidspell");
			return;
		endif

		//target where they want the roots
		cast_on := CanTargetArea(caster, circle);
		if (!cast_on)
			sendsysmessage(caster, "Canceled");
			EraseObjProperty (caster, "#castingdruidspell");
			return;
		endif
	endif

	//set the caster as the script controler and allow them to cast the next spell
	SetScriptController (caster);
	EraseObjProperty (caster, "#castingdruidspell");

	//create the summoned grasping roots
	var npcparms := {};
	npcparms .+ name := "grasping roots";
	var graspingroots := CreateNPCfromTemplate("corpser", cast_on.x, cast_on.y, cast_on.z, npcparms);
	if (!graspingroots)
		return;
	endif

	//if it was cast by a player, set the player as the creature's master
	if (!caster.npctemplate)
		graspingroots.setmaster (graspingroots);
	endif
	SetObjProperty (graspingroots, "summoned", 1);

	var magery := GetAttribute(caster,SKILLID_MAGERY);
	SetAttributeBaseValue (graspingroots, SKILLID_MAGICRESISTANCE, BaseSkillToRawSkill( magery*10) );
	PlaySoundEffect(caster, SFX_SPELL_SUMMON_ELEMENTAL);
	sleep(2);

	//create an event that will be sent later
	var ev Array;
	ev.+type := EVID_DAMAGED;
	ev.+source := 0;

	//every 2 seconds try to paralyze all the other creatures in the area
	var i;
	for ( i := 1; i <= 20; i := i + 1 )
		foreach critter in ListMobilesNearLocation (graspingroots.x, graspingroots.y, graspingroots.z, 1)
			if (critter != graspingroots)
				if (!checkskill(critter, SKILLID_MAGICRESISTANCE, -1, 0))
					ApplyDamage(critter, randomint(10));
                                        start_script(":spells:para", {critter, 10});
//					DoTempMod (critter, "p", 1, 10);
				endif
				ev.source := critter;
				SendEvent(graspingroots, ev);
			endif
		endforeach
		sleep(2);

		//make sure the grapsingroots are still alive
		if (!graspingroots)
			return;
		endif
	endfor

	if (graspingroots)
		PlaySoundEffect( graspingroots , SFX_SPELL_MANA_VAMPIRE );
		PlayStationaryEffect( graspingroots.x, graspingroots.y, graspingroots.z, FX_SMOKE, 0xA, 0xA );
		MoveCharacterToLocation(graspingroots,5288 , 1176 , 0,MOVECHAR_FORCELOCATION);
	        graspingroots.hidden := 1;
		ApplyRawDamage(graspingroots, 1000);
	endif

endprogram

