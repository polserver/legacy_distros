use uo;
use os;

include "include/client";
include "include/eventid";
include "include/spellRestrictions";

program stinging_swarm ( parms )

	//some variables
	var circle := 6;
	var caster;
	var cast_on;

	//split the parms, checking to see if it was cast by an NPC
	if (parms[1] = "#MOB")
		caster := parms[2];
		cast_on := parms[3];

	//otherwise it was cast by a player
	else
		caster := parms;

		//make sure they can cast it
		if (!Can_Cast (caster, circle) )
			EraseObjProperty (caster, "#castingdruidspell");
			return;
		endif

		//let them pick a target
		cast_on := CanTargetArea(caster, circle);
		if (!cast_on)
			sendsysmessage(caster, "Canceled");
			EraseObjProperty (caster, "#castingdruidspell");
			return;
		endif
	endif

	//set them as the script controlers and allow them to cast another spell now
	SetScriptController (caster);
	EraseObjProperty (caster, "#castingdruidspell");

	//create a swarm of bees and set the caster as their controller.  This is used in the control
	//script for the bees so that the caster has responsibility for casting the spell
	PlaySoundEffect(caster, SFX_SPELL_SUMMON_ELEMENTAL);
	var swarm := CreateItemAtLocation (cast_on.x, cast_on.y, cast_on.z, 0x091b, 1);
	SetObjProperty (swarm, "creator", caster.serial);

endprogram
