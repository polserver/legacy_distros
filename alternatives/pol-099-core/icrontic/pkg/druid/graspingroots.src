use uo;
use os;
use util;

include "include/client";
include "include/eventid";
include "include/spellRestrictions";

program groots( parms )
 
	var circle := 8;
	var caster;
	var cast_on;

	if (parms[1] = "#MOB")
		caster := parms[2];
		cast_on := parms[3];
	else
		caster := parms;
		cast_on := CanTargetArea(caster, circle);
		if (!cast_on)
			sendsysmessage(caster, "Canceled");
			return;
		endif
	endif

	SetScriptController( caster );

	var npcparms := {};
	npcparms .+ name := "grasping roots";
	var thecritter := CreateNPCfromTemplate("corpser", cast_on.x, cast_on.y, cast_on.z, npcparms);
	if (!thecritter)
		return;
	endif

	if (!caster.npctemplate)
		thecritter.setmaster( caster );
	endif

	SetObjProperty(thecritter, "summoned", 1);
	var magery := GetAttribute(caster,SKILLID_MAGERY);
	SetAttributeBaseValue (thecritter, SKILLID_MAGICRESISTANCE, BaseSkillToRawSkill( magery*10) );
	PlaySoundEffect(caster, SFX_SPELL_SUMMON_ELEMENTAL);
	sleep(2);
	var ev Array;
	ev.+type := EVID_DAMAGED;
	ev.+source := 0;

	var i;
	for ( i := 1; i <= 20; i := i + 1 )
		foreach critter in ListMobilesNearLocation( thecritter.x, thecritter.y, thecritter.z, 1 )
			if (critter != thecritter)
				if (!checkskill(critter, SKILLID_MAGICRESISTANCE, -1, 0))
					ApplyDamage(critter, randomint(10));
					critter.paralyzed := 1;
				endif
        	       		ev.source := critter;
				SendEvent(thecritter, ev);
			endif
		endforeach
		sleep(2);
		if (!thecritter)
			return;//it died
		endif
	endfor

	if (thecritter)
		PlaySoundEffect( thecritter , SFX_SPELL_MANA_VAMPIRE );
		PlayStationaryEffect( thecritter.x, thecritter.y, thecritter.z, FX_SMOKE, 0xA, 0xA );
		MoveCharacterToLocation(thecritter,5288 , 1176 , 0,MOVECHAR_FORCELOCATION);
		ApplyRawDamage(thecritter, thecritter.hp + 3);
	endif

endprogram

