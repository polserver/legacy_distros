use uo;
use os;

include "include/client";
include "include/spellRestrictions";
include "include/spellAttack";
include "/druid";

program tidal_wave ( parms )
	var circle := 8;
	var caster;
	var cast_on;

	if (parms[1] = "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		send_attack(cast_on,caster, 12);
	else
		caster := parms;
		cast_on := CanTargetSpell(caster, circle, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	if (Reflected(cast_on))
		cast_on := caster;
		if (GetObjProperty( caster, "mr") = "1")
			EraseObjProperty(caster, "mr");
			SendSysMessage(caster, "Your magic reflect spell saved you!");
			return;
		endif
	endif

	SetScriptController( caster );
	PlaySoundEffect(caster, SFX_SPELL_SUMMON_ELEMENTAL);
	var targettedx := cast_on.x;
	var targettedy := cast_on.y;
	var targettedz := cast_on.z;

	var wavetop1 := CreateItemAtLocation (targettedx, targettedy, targettedz+110, 0x350b, 1);
	var wavetop2 := CreateItemAtLocation (targettedx, targettedy, targettedz+110, 0x34ed, 1);
	wavetop1.movable := 0;
	wavetop2.movable := 0;

	var i;
	var waterfall_array := {};
	var waterfall_tile;

	for(i := 10; i >= 0; i := i - 1)
		waterfall_tile := CreateItemAtLocation (targettedx, targettedy, targettedz+i*10, 0x3517, 1);
		waterfall_tile.movable := 0;
		waterfall_array.append (waterfall_tile);
		waterfall_tile := CreateItemAtLocation (targettedx, targettedy, targettedz+i*10, 0x34f9, 1);
		waterfall_tile.movable := 0;
		waterfall_array.append (waterfall_tile);
		sleepms (50);
	endfor
	var waterfall_base1 := CreateItemAtLocation (targettedx,   targettedy+1, targettedz, 0x17a1, 1);
	waterfall_base1.movable := 0;
	var waterfall_base2 := CreateItemAtLocation (targettedx+1, targettedy,   targettedz, 0x179d, 1);
	waterfall_base2.movable := 0;
	var waterfall_base3 := CreateItemAtLocation (targettedx,   targettedy-1, targettedz, 0x179f, 1);
	waterfall_base3.movable := 0;
	var waterfall_base4 := CreateItemAtLocation (targettedx-1, targettedy,   targettedz, 0x17a3, 1);
	waterfall_base4.movable := 0;
	var waterfall_base5 := CreateItemAtLocation (targettedx+1, targettedy+1, targettedz, 0x17a7, 1);
	waterfall_base5.movable := 0;
	var waterfall_base6 := CreateItemAtLocation (targettedx-1, targettedy+1, targettedz, 0x17a8, 1);
	waterfall_base6.movable := 0;
	var waterfall_base7 := CreateItemAtLocation (targettedx+1, targettedy-1, targettedz, 0x17a5, 1);
	waterfall_base7.movable := 0;
	var waterfall_base8 := CreateItemAtLocation (targettedx-1, targettedy-1, targettedz, 0x17a6, 1);
	waterfall_base8.movable := 0;
	var waterfall_base9 := CreateItemAtLocation (targettedx,   targettedy,  targettedz, 0x1797, 1);
	waterfall_base9.movable := 0;

	var dmg := CalcSpellDamage(circle, caster, cast_on);
	send_attack(cast_on,caster, 49);
	ApplyRawDamage( cast_on, DruidResist( caster, cast_on, dmg) );

	PlaySoundEffect(caster, 0x11);
	sleep (5);

	DestroyItem (wavetop1);
	DestroyItem (wavetop2);

	foreach waterfall_tile in waterfall_array
		DestroyItem (waterfall_tile);
		sleepms(25);
	endforeach

	sleep(30);

	DestroyItem (waterfall_base1);
	DestroyItem (waterfall_base2);
	DestroyItem (waterfall_base3);
	DestroyItem (waterfall_base4);
	DestroyItem (waterfall_base5);
	DestroyItem (waterfall_base6);
	DestroyItem (waterfall_base7);
	DestroyItem (waterfall_base8);
	DestroyItem (waterfall_base9);

endprogram
