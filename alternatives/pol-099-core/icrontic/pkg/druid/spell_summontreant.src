use uo;
use os;
use util;

include "include/client";
include "include/eventid";
include "include/spellRestrictions";

program summon_wolves (parms)

	//some variables
	var circle := 8;
	var caster;
	var location;

	//if the spell was cast by an NPC
	if (parms[1] = "#MOB")
		caster := parms[2];
		location := parms[3];

	//otherwise it was cast by a player
	else
		caster := parms;

		//make sure they can cast it based on their armor
		if (!Can_Cast (caster, circle) )
			EraseObjProperty (caster, "#castingdruidspell");
			return;
		endif

		//pick a location for the treant
		location := CanTargetArea(caster, circle);
		if (!location)
			sendsysmessage(caster, "Canceled");
			EraseObjProperty (caster, "#castingdruidspell");
			return;
		endif
	endif

	//allow them to cast another spell now
	SetScriptController( caster );
	EraseObjProperty (caster, "#castingdruidspell");

	var treant;
	var summoning_parms := {};
	summoning_parms.+script := "tamed";
	summoning_parms.+name := "treant";

	PlayStationaryEffect( location.x, location.y, location.z, FX_SMOKE, 0xA, 0xA );
	PlaySoundEffect(caster, 0x022b);
	treant := CreateNPCfromTemplate("ent", location.x, location.y, location.z, summoning_parms);
	SetObjProperty(treant, "master", caster.serial);
	SetObjProperty(treant, "summoned", 1);
	treant.script := "tamed";
	RestartScript(treant);

	//detach and sleep
	detach();
	var summoned_time := 60 + GetAttribute (caster, SKILLID_ANIMALLORE);
	sleep (summoned_time);

	//kill the treant
	if (treant)
		PlaySoundEffect (treant, SFX_SPELL_MANA_VAMPIRE);
		PlayStationaryEffect( treant.x, treant.y, treant.z, FX_SMOKE, 0xA, 0xA );
		MoveCharacterToLocation(treant, 5288, 1176, 0, MOVECHAR_FORCELOCATION);
		damage(treant, treant.hp+3);
	endif

endprogram
