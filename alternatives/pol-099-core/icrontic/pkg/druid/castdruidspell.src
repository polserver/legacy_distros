use uo;
use os;
use cfgfile;

include "include/eventid";
include "include/client";
include "druid";
include "include/myutil";


program cast_druid_spell (parms)
	var spell_cfg_file := ReadConfigFile ("druidspells");
	var caster := parms [1];
	var staff := parms [2];
	var spell_number := parms [3];

	//check to see if they're already casting another spell
	if (GetObjProperty (caster, "#castingdruidspell") )
		SendSysMessage (caster, "You're already casting another spell!");
		return;
	endif

	//search to see if the player has the rune for that spell
	if (!SearchBackpackForRune (caster, spell_cfg_file[spell_number].rune_objtype) )
		SendSysMessage (caster, "You don't have the rune for that spell.");
		return;
	endif

	//this is what really casts the spell
	if (!castdruidspell(caster, staff, spell_number ))
		Fizzle(caster);
	endif
endprogram




///////////////////
//  searches the characters backpack for each druid rune bag they have and checks them until it
//  finds one that contains the given rune
///////////////////

function SearchBackpackForRune (character, rune_objtype )

	foreach bag in ListRootItemsInContainerOfObjtype (character.backpack, 0x706b)
		if (CheckForRuneInBag (character, bag, rune_objtype) )
			return 1;
		endif
	endforeach

	return 0;
endfunction
