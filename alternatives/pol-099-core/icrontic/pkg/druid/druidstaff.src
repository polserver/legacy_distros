use cfgfile;
use os;
use uo;

include "include/eventid";
include "include/client";
include "druid";


program druidstaff(staff)
	
	detach();
	StaffListener( staff );

endprogram




///////////////////
//  program that loops forever listening for words spoken by the character and trying
//  to match them to druid spells
///////////////////

function StaffListener( staff )

	//load info about the spells and set up other variables
	var spell_cfg_file := ReadConfigFile ("druidspells");
	var number_of_spells := GetConfigMaxIntKey (spell_cfg_file);
	var current_spell;
	var i;
	var last_reload_of_config := ReadGameClock();

	//allows the staff to listen
	RegisterForSpeechEvents( staff, 1 );
	EnableEvents( EVID_SPEECH, 1 );

	var event;
	var parms := {};

	//repeat forever
	while (1)
		//reload the spell config file every 5 minutes
		if (ReadGameClock() > last_reload_of_config + 300)
			spell_cfg_file := ReadConfigFile ("druidspells");
			number_of_spells := GetConfigMaxIntKey (spell_cfg_file);
		endif

		//wait for an event and process it when it happens
		event := wait_for_event( 120 );
        	if (event and event.type = EVID_SPEECH)
			//make sure the staff is equipped
			if (IsEquipped(event.source, staff))
				//if so, go through all the spells in the druidspells.cfg file looking for a match
				for (i := 1; i <= number_of_spells; i := i + 1)
					if (lower(spell_cfg_file[i].power_words) = lower (event.text) )
						//This is a valid spell.  start the castdruidspell script
						parms[1] := event.source;
						parms[2] := staff;
						parms[3] := i;
						start_script ("castdruidspell", parms);
					endif
				endfor

				//for GMs only
				if (lower(event.text) = "i do not need reagents" and event.source.cmdlevel)
					SetObjProperty (event.source, "#nodruidreagents", 1);
					SendSysMessage (event.source, "Reagent consumption off");
				elseif (lower(event.text) = "i need reagents" and event.source.cmdlevel)
					EraseObjProperty (event.source, "#nodruidreagents");
					SendSysMessage (event.source, "Reagent consumption on");
				elseif (lower(event.text) = "create corruption" and event.source.cmdlevel)
					CreateItemAtLocation (event.source.x, event.source.y, event.source.z, 0x706d, 1);
				elseif (lower(event.text) = "nigh tir")
					parms[1] := event.source;
					parms[2] := 0;
					start_script("purify", parms);
				elseif (lower(event.text) = "quick purify" and event.source.cmdlevel)
					parms[1] := event.source;
					parms[2] := 1;
					start_script("purify", parms);
				endif
			endif
        	endif
	endwhile

endfunction




///////////////////
//  makes sure that the staff is actually equiped and not listening in the player's
//  backpack or something
///////////////////

function IsEquipped(character, staff)

	var weaponone := GetEquipmentByLayer( character,  LAYER_HAND1 );
	var weapontwo := GetEquipmentByLayer( character,  LAYER_HAND2 );

	if ( ( weaponone = staff ) or (weapontwo = staff ) )
		return 1;
	else
		return 0;
	endif

endfunction




