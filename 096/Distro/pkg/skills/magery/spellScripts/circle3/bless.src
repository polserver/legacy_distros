/*
 * $Id: bless.src 319 2006-06-12 19:51:26Z austinheilman $
 *
 */

use uo;
use os;

include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":magery:spells";
include ":magery:spellSounds";
include "include/client";
include "include/sounds";

program SpellScript_Bless(params)
	var mobile := params[1];
	var info := params[2];
	params := 0; // No longer needed
	
	var targ := MS_Target(mobile, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	PlaySoundEffect(targ, SFX_SPELL_BLESS);
	PlayObjectCenteredEffect(targ, GFX_BLUE_SPARKLES, 7, 7);	
	
	// Duration: ((Eval / 5) + 1) * 6
	var duration := ((AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) / 5) + 1) * 6;
	
	// Same as casting agility, cunning and strength all at once.
	// I don't THINK they can stack with bless
	TS_StartTimer(targ, "agility", duration, 0, mobile);
	TS_StartTimer(targ, "cunning", duration, 0, mobile);
	TS_StartTimer(targ, "strength", duration, 0, mobile);
	
	return 1;
endprogram
