use uo;
use os;
use util;

include "include/client";

var spelldelay := 3;

program cast_raise_dead(caster)
    
    SpeakPowerWords(caster, 68);

	While (spelldelay > 0)
		PerformAction(caster, ANIM_CAST_DIR);
		spelldelay := spelldelay - 1;
		sleep(1);
	endwhile
		sleepms(500);

	if (!ConsumeMana(caster, 68))
		PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
		PlaySoundEffect(caster, SFX_5B);
		SendSysMessage(caster, "Insufficient Mana.");
		return;
	endif
	
	if (!ConsumeReagents(caster, 68))
		PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
		PlaySoundEffect(caster, SFX_5B);
		SendSysMessage(caster, "Insufficient Reagents.");
		return;
	endif

	if (!CheckSkill(caster, SKILLID_MAGERY, 70, 0))
		PlayObjectCenteredEffect(caster, FX_SPELL_FAIL, 7, 7);
		PlaySoundEffect(caster, SFX_5B);
		return;
	endif


    SendSysMessage(caster, "Select victim.");
    local victim := Target(caster);
    local amount:= 0;
    local victimID:=GetObjProperty(victim, "master");

    if (victimID=caster.serial)
    amount := cint(victim.hp / 10);
    else
	SendSysMessage(caster, "Invalid Target.");
	return;
    endif

    local CasterSkill := GetSkill(caster, SKILLID_MAGERY);
    local range := Cint(CasterSkill / 20);
    local targets := ListMobilesNearLocation(victim.x, victim.y, victim.z, range);
    local diestring := "4d" + Cstr(amount);

    

    foreach mobile in targets
	if ((mobile.serial != caster.serial) and (mobile.dead = 0) and (CheckLineOfSight(Victim, mobile)))
		PlaySoundEffect(mobile, SFX_208);
		PlayObjectCenteredEffect(mobile, 0x036b0, 7, 0x10);
		local dmg := RandomDiceRoll(diestring);
		ApplyRawDamage(mobile, dmg);
	endif
    endforeach

    PlayLightningBoltEffect(victim);
    ApplyRawDamage(victim, victim.hp+3);

endprogram
