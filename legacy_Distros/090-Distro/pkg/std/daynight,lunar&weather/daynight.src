// 2/6/99 Day/Night transitions for one time zone only (britain) -Rac
// 23/4/00 played around with a little bit -Rax


use uo;
use os;

set_critical( 1 );

Include "include/clock";
Include "moonlight";

Const NIGHT_NUMERATOR := 1.0;
Const DENOMINATOR := 4;


global length := (Cint(GetDayLength()) * 60);
global transtime := ( length * (0.25) );

program DNCycle()

Print("Starting Day/Night cycle...");

local time;
local wstuff := GetGlobalProperty("Weather");

if (!wstuff)
	SetGlobalProperty("Weather", {0, 0, 0, 0, 27, 0, 0, 0});
endif
while(1)
	time := Cint(GetTime());

	if( (time >= 0) AND (time < transtime))
		Broadcast("Darkness envelopes the land.");
		wstuff := GetGlobalProperty("Weather");
		SetRegionLightLevel("Background", wstuff[5]);
		wstuff[6] := wstuff[5];
	   	wstuff[3] := 0;
		SetGlobalProperty("Weather", wstuff);
		sleep(transtime);
 
        elseif( (time >= (transtime)) AND (time < (length / 2)))
	   	Broadcast("Dawn approaches.");
		wstuff := GetGlobalProperty("Weather");
		wstuff[3] := 2;
		SetGlobalProperty("Weather", wstuff);
	  	DawnTransition(wstuff);

 	elseif( (time >= (length - transtime)) AND (time < length))
	   	Broadcast("The sun begins to set.");
		SetGlobalProperty("day", 2);
		wstuff := GetGlobalProperty("Weather");
		wstuff[3] := 2;
		SetGlobalProperty("Weather", wstuff);
	   	DuskTransition(wstuff);

	else
	   	Broadcast("A new day begins.");
		wstuff := GetGlobalProperty("Weather");
		DoMoons(wstuff);
		ChangeMoonlightSet(wstuff);
	   	SetRegionLightLevel( "Background" , wstuff[4] );
		wstuff[6] := wstuff[4];
	   	SetGlobalProperty("Weather", wstuff);
		sleep(transtime);

	endif

endwhile

endprogram