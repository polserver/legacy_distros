/////////////////////////////////////////////////////////////////////////////
//
//  meek.src: Run from nasty players
//
//  TODO: Some server support for only paying attention to 'enteredarea'
//        and 'leftarea' events from PCs would be nice.
//        we just use the graphic at the moment, so we'd fight
//        NPCs too.  Oh well.
//
//  Author: Racalac, adapted from ENS' "killpcs.src"
//
//  Created: 4/98
//
/////////////////////////////////////////////////////////////////////////////

use npc;
use os;
use uo;

include "include/eventid";
include "include/randname";
include "include/npcutil";
include "include/anchors";

const HALT_THRESHOLD := 2; // how close before he barks?

// TODO: move these to OBJTYPE.INC

const UOBJ_HUMAN_MALE   := 0x190;
const UOBJ_HUMAN_FEMALE := 0x191;

global me := Self();
if( me.name["<random>"] )
   SetName ( me , RandomName( me ) );
endif


program Aloof()
drop_anchor();
    EnableEvents( EVID_SPEECH + EVID_ENGAGED + EVID_DISENGAGED + EVID_DAMAGED,HALT_THRESHOLD);
    EnableEvents( EVID_ENTEREDAREA + EVID_LEFTAREA, HALT_THRESHOLD );
    EnableEvents( EVID_OPPONENT_MOVED );
 
	if (getobjproperty(me, "frozen"))
		me.frozen := 1;
	endif

    SetWarMode( 0 );
    set_priority(20);

    local ev;
    while (1)
        ev := os::wait_for_event( 10 );
        // print( "event type: " + ev.type );
        case (ev.type)
            EVID_SPEECH:
                // We ignore speech
                break;

            EVID_ENGAGED:
                Fight( ev.source );
            EVID_PEACEMADE:
                        break;
            EVID_DAMAGED:
                if (ev.source)
                    Fight( ev.source );
                endif

            EVID_ENTEREDAREA:
                if (ev.source.graphic in array( UOBJ_HUMAN_MALE, UOBJ_HUMAN_FEMALE ))
                    Fight( ev.source );
                endif

        endcase	

	NPCwander();
    endwhile
endprogram

/////////////////////////////////////////////////////////////////////////////
//
//  These types fight until you damage them enough or back away
//
/////////////////////////////////////////////////////////////////////////////

function Fight( opponent )

    DisableEvents( EVID_ENTEREDAREA + EVID_LEFTAREA );
    SetOpponent( opponent );

    local waittime;
  outer:
    while (opponent and !opponent.dead)
        waittime := CloseDistance( opponent ) * 15;

        local ev := wait_for_event( waittime );
        // print( "event type: " + ev.type );
        case (ev.type)
            EVID_SPEECH:
                break;
            
            EVID_DISENGAGED:
                    SetWarMode( 0 );
                break outer;

            EVID_ENGAGED:
            EVID_DAMAGED:
		if( me.hp <= ( 0.1 * me.maxhp ) )
		    OpenDistance( opponent );
		    SetWarMode( 0 );
		endif
                break outer;
            EVID_PEACEMADE:
                        opponent := 0;
                        break;
            EVID_OPPONENT_MOVED:
                break; // just waking up is enough
                
            EVID_ENTEREDAREA:
            EVID_LEFTAREA:
                // ignore both of these
        endcase
    endwhile

    EnableEvents( EVID_ENTEREDAREA + EVID_LEFTAREA, HALT_THRESHOLD );
    SetWarMode( 0 );
endfunction


/////////////////////////////////////////////////////////////////////////////
//
//  CloseDistance - close the distance between self and an opponent.
//  Returns: 1 if distance is 0 or 1 (no move necessary)
//           0 if distance is >= 2 (may still need to move)
//
//  This is a .EM-file candidate.
//
/////////////////////////////////////////////////////////////////////////////
function CloseDistance( opponent )
    case Distance( me, opponent )
        1:              // the most likely, so first 
        0:
            return 1;
        
        2:
            WalkToward( opponent );
            return 0;

        default:
            RunToward( opponent );
            return 0;
    endcase
endfunction

/////////////////////////////////////////////////////////////////////////////
//
//  OpenDistance - open the distance between self and an opponent.
//  Returns: 1 if distance is 10 (no move necessary)
//           0 if distance is < 10 (may still need to move)
//
/////////////////////////////////////////////////////////////////////////////
function OpenDistance( opponent )
    case Distance( me, opponent )
        10:
            return 1;
        
        default:
            RunAwayFrom( opponent );
            return 0;
    endcase
endfunction
