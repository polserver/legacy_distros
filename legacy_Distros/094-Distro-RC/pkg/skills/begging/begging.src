
use uo;
use util;
use cfgfile;

include "include/attributes";
include "include/client";
include "include/objtype";

const MINKARMA := 4000;
const MINFAME := 4000;

var zPleas := {
  "Spare some change?",
  "Alms for the poor?",
};

var zFails := {
  "I think not",
  "Go away!",
  "Off with you, peasant!",
  "Thou dost not look trustworthy... No gold for thee today!",
  "I feel sorry for thee... Thou dost not look trustworthy... No gold for thee today!",
  "Ye wont be getting any gold from me today"
};

var zBroke := {
  "Ain't got nothin' t' give ya!",
  "No' got nothin'. Sorry.",
  "No much better off meself'.",
  "Maybe ye can spare a coin for me?"
};

var zEmpty := {
  "Sorry, gave it all away already.",
  "Ain't got nothin' t' give ya!",
  "Nope. Got nothing. Sorry.",
  "Your cohorts cleaned me out already!"
};

var zNone := {
  "I'm sorry, I can give thee nothing.",
  "Does it look like I have pockets in this armor?",
  "Not while I'm on duty."
};

var zLowKarma := {
  "I will not give money to such as thee!",
  "Piss off!",
  "I do not give money to naughty evildoers!",
  "Thou dost not look trustworthy... no gold for thee today!"
};

const UOBJ_GOLDCOIN :=0x0eed;

program skill_begging( who )
  SendSysMessage( who, "Select the victim" );
  var tgt := Target( who );
  if ( !tgt )
    SendSysMessage( who, "Cancelled" );
    return;
  endif
  var cfgtemplate := ReadConfigFile("::npcdesc");
  var ismob := tgt.isa(POLCLASS_MOBILE);
  if (ismob)
    PrintTextAbove(who, "*looks at " + tgt.name + "*" );
  else
    PrintTextAbove( who, "*looks at " + tgt.desc + "*" );
  endif
  PrintRandomMessage( who, zPleas );
  if ((!ismob) || (!tgt.isa(POLCLASS_NPC)))
    return;
  endif
  sleep(1);
  if (GetObjProperty(who, "Karma") < 0)
    PrintRandomMessage( tgt, zLowKarma );
    return;
  endif
  var wealth := cfgtemplate[tgt.npctemplate].wealth;
  if (!wealth)
    PrintTextAbovePrivate(who, "This creature will not give thee money!", who);
    return;
  endif
  if ( !CheckSkill( who, SKILLID_BEGGING, -1, 30 ) )
    PrintRandomMessage( tgt, zFails );
    return;
  endif
  var lb := Cint(GetObjProperty(tgt, "begpurse"));
  if(!lb)
    lb := 50;
    SetObjProperty(tgt, "begpurse", lb);
  endif
  if(lb < 25)
    PrintRandomMessage( tgt, zEmpty );
    return;
  endif
  var gold;
  case (wealth)
    "upper":
      gold := RandomDiceRoll("4d5");
    "middle":
      gold := RandomDiceRoll("3d4");
    "poor":
      gold := RandomDiceRoll("2d3");
    "broke":
    default:
      PrintRandomMessage( tgt, zBroke );
      return;
    "none":
      PrintRandomMessage( tgt , zNone );
      return;
  endcase
  if ((GetObjProperty(who, "Karma") > MINKARMA) and (GetObjProperty(who, "Fame") > MINFAME))
    gold := CInt(gold*1.3);
  endif
  lb := Cint(lb - gold);
  SetObjProperty(tgt, "begpurse", lb);
  PrintTextAbove( tgt, "*throws a few coins to " + who.name + "*" );
  if ( !CreateItemInBackpack( who, UOBJ_GOLDCOIN, gold ) )
    SendSysMessage( who, "You miss and the coins bounce into cracks in the ground!" );
  else
    var coin := "coins";
    if (gold == 1)
      coin := "coin";
    endif
    SendSysMessage( who, "You quickly stuff " + gold + " " + coin + " in your pack!" );
  endif
  if (GetObjProperty(who, "Karma") > 10)
    SetObjProperty(who, "Karma", GetObjProperty(who, "Karma")-10);
  endif
  return;
endprogram

function PrintRandomMessage( speaker, byref zSpeech )
  if(zSpeech.size())
    PrintTextAbove(speaker, zSpeech[RandomInt(zSpeech.size())+1]);
  endif
  return;
endfunction
