use npc;
use basic;
use os;
use uo;
use util;

include "util/bank";
include "include/attributes";
include "include/eventID";
include "include/sysEvent";
include "include/randName";
include "include/vetement";
include "include/utility";
include "include/mrcSpawn";
include "include/dist";
include "include/client";
include "include/NPCBackpacks";
include "include/yesNo";

const UOBJ_GOLD_COIN := 0xeed;
const STARTHAIR := 0x2030;
const ENDHAIR := 0x2060;

set_priority( 50 );

var inv_fs, inv_pb;
var me := Self();
var master := CInt(GetObjProperty(me, "master"));
var cont := GetObjProperty(me, "cont");
var firstname;

set_critical(1);
  var vendors := GetGlobalProperty("pcvendors");
  if(!vendors)
    vendors := {};
  endif
  if(me.serial in vendors)
  else
	vendors.append(me.serial);
  endif
  SetGlobalProperty("pcvendors", vendors);
set_critical(0);

program merchant()
  Detach();
  run_script_to_completion("NPCKeeper", me);
  start_script(":playervendor:payCounter", me);
  var nme := SplitWords(me.name);
  firstname := lower(nme[1]);
  var oldpack := me.backpack;
  if(oldpack.objtype != 0x966c)
    MoveItemToLocation(oldpack, 5851, 1161, 0, MOVEITEM_FORCELOCATION);
    var newpack := CreateItemAtLocation(5851, 1162, 0, 0x966c, 1);
    EquipItem(me, newpack);
    foreach thing in ListRootItemsInContainer(oldpack)
      MoveItemToContainer(thing, newpack);
    endforeach
    DestroyItem(oldpack);
  endif
  inv_pb := find_or_create_in_pack(me.serial + " PB", 0xe75);
  inv_fs := find_or_create_in_pack(me.serial + " 1C", 0xe75);
  SetObjProperty(inv_pb, "Vendor", me.serial);
  SetObjProperty(inv_fs, "Vendor", me.serial);
  SetObjProperty(inv_pb, "Vendored", me.serial);
  SetObjProperty(inv_fs, "Vendored", me.serial);
  SetObjProperty(inv_pb, "Master", master);
  SetObjProperty(inv_fs, "Master", master);
  if(!inv_fs || !inv_pb || !inv_fs)
    exit;
  endif
  SetObjProperty(me,"serial", me.serial);
  EnableEvents( SYSEVENT_ITEM_GIVEN + SYSEVENT_MERCHANT_SOLD + SYSEVENT_DOUBLECLICKED);
  EnableEvents( SYSEVENT_SPEECH, 4);
  var res;
  var ev;
  var text;
  while(1)
    ev := os::wait_for_event(120);
    if(ev)
      case (ev.type)
	    SYSEVENT_DOUBLECLICKED: SendOpenSpecialContainer(ev.source, inv_fs);
        SYSEVENT_SPEECH:        text := lower(ev.text);
                            if (text["buy"])
                              TurnToward( ev.source );
                              if(!CheckLineOfSight(me, ev.source))
                                say("Who is speaking? I cannot see you.");
                              else
                                process_sale(ev.source);
                              endif
                            elseif(text["purchase"])
                              if(ev.source.serial == master)
                                addthingstobuy();
                              endif
                            elseif((text[firstname]) && (text["dismiss"]))
                              TurnToward( ev.source );
                              if (ev.source.serial == master)
                                if(YesNo(ev.source, "Fire him?"))
                                  Fireme();
                                endif
                              else
                                say("I don't even work for you.");
                              endif
                            elseif (text["sell"])
                              TurnToward( ev.source );
                              SpoutBuyList(ev.source);
                            elseif(text["contact"])
                              TurnToward( ev.source );
                              if(ev.source.serial == master)
                                cont := recontact(ev.source);
                                SetObjProperty(me, "cont", cont);
                              else
                                say("You may contact my employer " + cont);
                              endif
                            elseif((text["collect"]) && (ev.source.serial == master))
                              TurnToward( ev.source );
                              cashout(ev.source);
                            elseif((text["gold"]) && (ev.source.serial == master))
                              TurnToward( ev.source );
                              cashout(ev.source);
                            elseif((text["status"]) && (ev.source.serial == master))
                              TurnToward( ev.source );
                              statuscheck(ev.source);
                            elseif((text["info"]) && (ev.source.serial == master))
                              TurnToward( ev.source );
                              statuscheck(ev.source);
                            elseif((text["wear"]) && (ev.source.serial == master))
                              TurnToward( ev.source );
                              say("What shall I wear?");
                              var this := Target(ev.source);
                              wear_item( this);
                            elseif((text["strip"]) && (ev.source.serial == master))
                              TurnToward( ev.source );
                              say("Ooh, I think I feel a draft!");
                              strip_items(ev.source);
                            elseif (text["help"])
                              TurnToward( ev.source );
                              say("I will obey the following orders:");
                              sleep(1);
                              say("dismiss, sell, contact, collect,");
                              sleep(2);
                              say("wear, strip, status and purchase");
                            endif
        SYSEVENT_ITEM_GIVEN:    if(ev.source.serial == master)
                              stockitem(ev);
                            else
                              buyitem(ev.source, ev.item);
                            endif
        SYSEVENT_MERCHANT_SOLD: clearance(ev.source);
      endcase
      sleep(1);
    endif
    if(GetObjProperty(me,"deadbeat"))
      Fireme();
    endif
  endwhile
endprogram

function clearance(who)
  foreach trg in ListRootItemsInContainer(who.backpack)
    trg.name := GetObjProperty(trg,"oldname");
    if(trg.isa(POLCLASS_CONTAINER))
      foreach thing in EnumerateItemsInContainer(trg)
        thing.name := GetObjProperty(thing,"OldName");
        EraseObjProperty(thing, "OldName");
        EraseObjProperty(thing, "Vendored");
        EraseObjProperty(thing, "price");
        EraseObjProperty(thing, "Owner");
      endforeach
    endif
    EraseObjProperty(trg, "Master");
    EraseObjProperty(trg, "price");
    EraseObjProperty(trg, "OldName");
    EraseObjProperty(trg, "Vendored");
    EraseObjProperty(trg, "Owner");
  endforeach
endfunction

function statuscheck(who);
  var gold := CInt(GetObjProperty(me, "g"));
  var rsrv := CInt(GetObjProperty(me, "r"));
  var fees := 0;
  var mypay := 50;
  foreach item in inv_fs
    fees := fees + GetObjProperty(item,"price");
  endforeach
  mypay := mypay + Cint(fees / 100);
  var gamedays := (gold + rsrv) / mypay;
  var realdays := gamedays / 8;
  if(realdays == 1)
    say("I have enough gold to continue working for " + gamedays + " days. [ " + realdays + " real world day]");
  else
    say("I have enough gold to continue working for " + gamedays + " days. [ " + realdays + " real world days]");
  endif
endfunction

function process_sale(who);
  var gold := GetObjProperty(me,"g");
  if(Distance(me, who) > 3)
    say("You must come closer if you wish to purchase something.");
    return;
  endif
  say("What do you wish to purchase?");
  var trg := Target(who);
  if(!trg)
    SendSysMessage(who,"cancelled");
    return;
  endif
  var ok := 0;
  foreach item in EnumerateItemsInContainer(inv_fs)
    if(trg == item)
      ok := 1;
      break;
    endif
  endforeach
  if(ok == 0)
    say("You cannot purchase that");
    return;
  elseif(ok == 1)
    var price := CInt(GetObjProperty(trg, "price"));
    if((!price) || (price == error))
      say("Im sorry, but that is not for sale.");
      return;
    endif
    if(GetPayment(who, price))
      PrintTextAbovePrivate(me,"I have withdrawn " + price + " gold from your backpack.", who);
      SetObjProperty(me,"g", (price + gold));
    else
      var bankbox := FindBankBox(who);
      var contents := EnumerateItemsInContainer(bankbox);
      var amt := 0;
      foreach item in contents
        if((item.objtype == 0xeed) && (item.amount >= price))
          if(SubtractAmount(item, price))
            amt := 1;
            break;
          endif
        endif
      endforeach
      if(amt == 1)
        PrintTextAbovePrivate(me,"I have withdrawn " + price + " gold from your bank account.", who);
        SetObjProperty(me,"g", (price + gold));
      else
        PrintTextAbovePrivate(me,"You cannot afford this item.", who);
      endif
    endif
  endif
endfunction

function stockitem(tev)
  if (tev.item.objtype == UOBJ_GOLD_COIN)
    TakeGold(tev);
    return;
  endif
  var tname;
  var item := tev.item;
  SetObjProperty(item, "OldName", item.name);
  PrintTextAbovePrivate(me, "How much shall I charge for this item? ", tev.source);
  var cost := CInt(RequestInput(tev.source, item, ""));
  if(cost == 0)
    if(item.isa(POLCLASS_CONTAINER))
      if((item.objtype == 0x1940) || (item.objtype == 0xfea) || (item.objtype == 0x6100))
        say("Well, just keep it, then");
        if(item.isa(POLCLASS_CONTAINER))
          foreach thing in EnumerateItemsInContainer(item)
            thing.name := GetObjProperty(thing,"OldName");
            EraseObjProperty(thing, "OldName");
            EraseObjProperty(thing, "Vendored");
            EraseObjProperty(thing, "price");
          endforeach
        endif
        EraseObjProperty(item, "Master");
        EraseObjProperty(item, "price");
        EraseObjProperty(item, "OldName");
        EraseObjProperty(item, "Vendored");
        MoveItemToContainer(item, tev.source.backpack);
        return;
      else
        var items := 0;
        foreach thing in EnumerateItemsInContainer(item)
          items := 1;
          break;
        endforeach
        if(items == 1)
          PrintTextAbovePrivate(me, "Well, just keep it, then", tev.source);
          if(item.isa(POLCLASS_CONTAINER))
            foreach thing in EnumerateItemsInContainer(item)
              thing.name := GetObjProperty(thing,"OldName");
              EraseObjProperty(thing, "OldName");
              EraseObjProperty(thing, "Vendored");
              EraseObjProperty(thing, "price");
            endforeach
          endif
          EraseObjProperty(item, "Master");
          EraseObjProperty(item, "price");
          EraseObjProperty(item, "OldName");
          EraseObjProperty(item, "Vendored");
          MoveItemToContainer(item, tev.source.backpack);
          return;
        endif
        PrintTextAbovePrivate(me, "How shall I advertise this item?", tev.source);
        tname := RequestInput(tev.source, item, "");
        SetObjProperty(item, "price", "Not For Sale");
        cost := "Not For Sale";
      endif
    else
      say("Well, just keep it, then");
      if(item.isa(POLCLASS_CONTAINER))
        foreach thing in EnumerateItemsInContainer(item)
          thing.name := GetObjProperty(thing,"OldName");
          EraseObjProperty(thing, "OldName");
          EraseObjProperty(thing, "Vendored");
          EraseObjProperty(thing, "price");
        endforeach
      endif
      EraseObjProperty(item, "Master");
      EraseObjProperty(item, "price");
      EraseObjProperty(item, "OldName");
      EraseObjProperty(item, "Vendored");
      MoveItemToContainer(item, tev.source.backpack);
      return;
    endif
  endif
  if(item.isa(POLCLASS_CONTAINER))
    var theoldname;
    foreach thing in EnumerateItemsInContainer(item)
      theoldname := thing.desc;
      if(!item.desc)
        theoldname := "";
      endif
      SetName(thing, thing.desc + " [Cost: Not For Sale]");
      SetObjProperty(thing, "OldName", theoldname);
      SetObjProperty(thing, "Vendored", me.serial);
      SetObjProperty(thing, "price", "Not For Sale");
    endforeach
    SetObjProperty(item, "Master", master);
  endif
  var oldname := item.desc;
  if(!item.desc)
    oldname := "";
  endif
  SetObjProperty(item, "OldName", oldname);
  SetObjProperty(item, "price", CInt(cost));
  SetObjProperty(item, "Vendored", me.serial);
  if(tname)
    SetName(item, tname + " [Cost: " + cost + " ]");
  else
    SetName(item, item.desc + " [Cost: " + cost + " ]");
  endif
  if(!tev.vendor)
    MoveItemToContainer(item, inv_fs);
  endif
  if(cost == "Not For Sale")
    PrintTextAbovePrivate(me, "I'll just hang on to that for ya.", tev.source);
  else
    PrintTextAbovePrivate(me, "I will sell that for " + cost + " gold.", tev.source);
  endif
endfunction

function cashout(person)
  var mygold := CInt(GetObjProperty(me, "g"));
  var payment := 0;
  var rsrv := GetObjProperty(me,"r");
  var pbank := FindBankBox(person);
  if(mygold > 0)
    while(mygold > 60000)
      CreateItemInContainer(pbank, UOBJ_GOLD_COIN, 60000);
      payment := payment + 60000;
      mygold := mygold - 60000;
    endwhile
    payment := payment + mygold;
    say("Ive placed " + payment + " gold coins in your bank box.");
    CreateItemInContainer(pbank, UOBJ_GOLD_COIN, mygold);
    SetObjProperty(me, "g", 0);
  else
    say("I am not holding any gold for you.");
  endif
  if(rsrv)
    say("You have left me " + rsrv + " reserve gold coins to use towards my future wages.");
  else
    say("but you have left me nothing towards my future wages.");
  endif
endfunction

function recontact(who)
  var cnt := SendTextEntryGump( who, "How shall I tell people to contact you? ", TE_CANCEL_DISABLE, TE_STYLE_NORMAL, 50);
  return cnt;
endfunction

function wear_item(item)
  foreach thing in ListEquippedItems(me)
    MoveItemToContainer(thing, inv_pb);
  endforeach
  EquipItem(me, item);
  foreach thingie in EnumerateItemsInContainer(inv_pb)
    EquipItem(me, thingie);
  endforeach
  foreach thingie in EnumerateItemsInContainer(inv_pb)
    MoveItemToLocation(thingie, me.x, me.y, me.z, MOVEITEM_FORCELOCATION );
  endforeach
endfunction

function strip_items(mmaster)
  foreach thing in ListEquippedItems(me)
    if ((thing.objtype < STARTHAIR) || (thing.objtype > ENDHAIR))
      MoveItemToContainer(thing, inv_pb);
    endif
  endforeach
  foreach thing in EnumerateItemsInContainer(inv_fs)
    if (thing.container.serial == inv_fs.serial)
      MoveItemToContainer(thing, inv_pb);
    endif
  endforeach
  foreach thingie in EnumerateItemsInContainer(inv_pb)
    if (thingie.container.serial == inv_pb.serial)
      MoveItemToContainer(thingie, mmaster.backpack);
    endif
  endforeach
endfunction

function Fireme()
  var mypack := findmypack(me.serial);
  var mygold := CInt(GetObjProperty(me, "g"));
  var payment := 0;
  if(mygold > 0)
    while(mygold > 60000)
      CreateItemInContainer(mypack, UOBJ_GOLD_COIN, 60000);
      payment := payment + 60000;
      mygold := mygold - 60000;
    endwhile
    CreateItemInContainer(mypack, UOBJ_GOLD_COIN, mygold);
  endif
  foreach thing in ListRootItemsInContainer(inv_fs)
    MoveItemToContainer(thing, mypack);
  endforeach
  foreach thingy in ListRootItemsInContainer(inv_pb)
    MoveItemToContainer(thingy, mypack);
  endforeach
  RevokePrivilege( me, "invul" );
  say("I regret nothing!");
  Damage( me, GetHp(me) +3);
endfunction

function addthingstobuy()
    var buylist := {};
    var buyprice := { };
    var buyobj := { };
    var i;
    if (!GetObjProperty(me,"buylist"))
        buylist := {};
        buyprice := {};
        buyobj := { };
    else
        buylist := GetObjProperty(me,"buylist");
        buyprice := GetObjProperty(me,"buyprice");
        buyobj := GetObjProperty(me,"buyobj");
    endif
    say( "What do you want me to buy?" );
    var what := Target( master );
        if ((!what) || (what == error) || (GetStrength(what)) )
        say( "Nevermind" );
        return;
        endif
    if (what.desc in buylist)
        say("I will no longer purchase "+what.desc);
        var newbuylist := { };
        var newbuyprice := { };
        var newbuyobj := { };
        for (i := 1; i<=len(buylist); i:=i+1)
            if (buylist[i] != what.desc)
                newbuylist[len(newbuylist)+1] := buylist[i];
                newbuyprice[len(newbuyprice)+1] := buyprice[i];
                newbuyobj[len(newbuyobj)+1] := buyobj[i];
            endif
        endfor
        SetObjProperty(me,"buylist",newbuylist);
        SetObjProperty(me,"buyprice",newbuyprice);
        SetObjProperty(me,"buyobj",newbuyobj);
        return;
    endif
    if (!what.objtype)
        say("I can't buy those.  What the hell is that, anyway?");
        return;
    endif
    buylist[ len(buylist)+1] := what.desc;
    buyprice[ len(buyprice)+1] := CInt(SendTextEntryGump( master,
            "How much shall I pay for this item? "
, TE_CANCEL_ENABLE,
                        TE_STYLE_NORMAL));
    buyobj[ len(buyobj)+1] := what.objtype;
    if (len(buylist)==1)
        say("I will purchase "+buylist[1]);
    elseif (len(buylist)==2)
        say("I will purchase "+buylist[1] +" and "+ buylist[2]);
    else
        var buying := "I will purchase "+buylist[1];
        for (i := 2; i<=len(buylist)-1; i:=i+1)
            buying := buying + ", " +buylist[i];
        endfor
        buying := buying + " and " + buylist[len(buylist)];
        say(buying);
    endif
    SetObjProperty(me,"buylist",buylist);
    SetObjProperty(me,"buyprice",buyprice);
    SetObjProperty(me,"buyobj",buyobj);
endfunction

function SpoutBuyList(you)
  var buylist := {};
  var buyprice := {};
  var i;
  if (!GetObjProperty(me,"buylist"))
    say("I don't buy anything.");
    return;
  else
    buylist := GetObjProperty(me,"buylist");
    buyprice := GetObjProperty(me,"buyprice");
  endif
  for (i := 1; i<=len(buylist); i:=i+1)
    say("I'll pay "+ buyprice[i] + " for " + buylist[i]);
    sleep(2);
  endfor
  say( "Just hand me what you want me to buy." );
endfunction

function buyitem(you, it)
    var mypack := findmypack(me.serial);
    var itsownerserial := CInt(GetObjProperty(it, "ownerserial"));
    if (itsownerserial == CInt(master))
        say("I'll see that this is returned to my employer.");
        MoveItemToContainer(it,mypack);
        NotifyMaster(you.name + " gave me an item of yours.");
        return;
    endif
    var buylist := {};
    var buyprice := { };
    var buyobj := { };
    var i;
    if (!GetObjProperty(me,"buylist"))
        say("I don't buy anything");
        MoveItemToContainer(it,you.backpack);
        return;
    else
        buylist := GetObjProperty(me,"buylist");
        buyprice := GetObjProperty(me,"buyprice");
        buyobj := GetObjProperty(me,"buyobj");
    endif
    if (!(it.objtype in buyobj))
        say("Ugh, I don't want that.");
        MoveItemToContainer(it,you.backpack);
        return;
    endif
    var index := 0;
    for (i := 1; i<=len(buylist); i:=i+1)
        if (it.objtype == buyobj[i])
            index := i;
            break;
        endif
    endfor
    if (!index)
        say("Ugh, I don't want that.");
        MoveItemToContainer(it,you.backpack);
        return;
    endif
    var mygold := CInt(GetObjProperty(me, "g"));
    if ( (mygold-250) < (buyprice[index]*it.amount))
        say("Sorry, I can't afford that right now.");
        MoveItemToContainer(it,you.backpack);
        return;
    endif
    var itamount := it.amount;
    var itdesc := it.desc;
    if ( grabitem(it,mypack) )
        say("Thank you!  Here's your " + (buyprice[index]*itamount) + " gold.");
            CreateItemInContainer(you.backpack, UOBJ_GOLD_COIN, buyprice[index]*itamount );
        NotifyMaster(you.name + " just sold me " + itdesc);
    else
        NotifyMaster("My backpack is full!");
        MoveItemToContainer(it,you.backpack);
        return;
    endif
    var theprice := buyprice[index]*itamount;
    var mypay := CInt(GetObjProperty(me,"mypay"));
    var commission := CInt( 0.05 * theprice);
    mygold := mygold - theprice;
    mypay := mypay + commission;
    if (mygold >= mypay)
        mypay := 0;
        mygold := mygold - mypay;
    else
        mypay := mypay - mygold;
        mygold := 0;
    endif
    SetObjProperty(me, "mypay",mypay);
    SetObjProperty(me, "g", mygold);
endfunction

function NotifyMaster(themessage)
    foreach onlinechr in EnumerateOnlineCharacters()
        if (onlinechr.serial == master)
        SendSysMessage(onlinechr, me.name + " : " + themessage);
        break;
        endif
    endforeach
endfunction

function TakeSale(goldamount, whoserial)
    var mypay := CInt(GetObjProperty(me, "mypay"));
    var mygold := CInt(GetObjProperty(me, "g"));
    var commission := CInt( 0.05 * goldamount);
    if (whoserial == master)
        if (!mypay)
            say("Since I work for you, I won't take a commission on that.");
            commission := 0;
        else
            say("Normally I wouldn't take commission on that, but you owe me "+ mypay + " gold!");
        endif
    endif
    mypay := mypay + commission;
    goldamount := goldamount - commission;
    mygold := mygold + goldamount;
    if (mygold >= mypay)
        mypay := 0;
        mygold := mygold - mypay;
    else
        mypay := mypay - mygold;
        mygold := 0;
    endif
    SetObjProperty(me, "mypay",mypay);
    SetObjProperty(me, "g", mygold);
endfunction

function TakeGold(ev)
    var goldamount := ev.item.amount;
    var mygold := CInt(GetObjProperty(me, "r"));
    if(goldamount)
      mygold := mygold + goldamount;
      var totalgold := CInt(GetObjProperty(me,"g")) + mygold;
      say("I am holding " + totalgold + " gold for you.");
    endif
    SetObjProperty(me, "r", mygold);
    DestroyItem(ev.item);
endfunction

function grabitem(it,mypack)
  var mystuff := EnumerateItemsInContainer(mypack);
  if (it.amount > 1 )
    foreach item in mystuff
      if (item.objtype == it.objtype)
        if (CreateItemInContainer( mypack, it.objtype, it.amount ))
          DestroyItem(it);
          return 1;
        else
          say("My backpack is full!");
          return 0;
        endif
      endif
    endforeach
  else
    foreach item in mystuff
      if ((item.objtype == it.objtype) && (item.amount > 1 ))
        if ( CreateItemInContainer( mypack, it.objtype, it.amount ) )
          DestroyItem(it);
          return 1;
        else
          say("My backpack is full!");
          return 0;
        endif
      endif
    endforeach
  endif
  if (MoveItemToContainer(it,mypack))
    return 1;
  else
    return 0;
  endif
endfunction

function GetPayment(player, price)
  var backpack := EnumerateItemsInContainer(player.backpack);
  var a := 0;
  foreach item in backpack
    if ((item.objtype == 0xeed) && (item.amount >= price) )
      if(SubtractAmount(item, price))
        a := 1;
        break;
      endif
    endif
  endforeach
  if(a == 1)
    return 1;
  else
    say("You dont have " + price + " gold coins!");
    return 0;
  endif
endfunction

function find_or_create_in_pack(name, objtype);
  var mypack := me.backpack;
  var check := 0;
  foreach thing in ListRootItemsInContainer(mypack)
    if(thing.name == name)
      check := thing;
      break;
    endif
  endforeach
  if(check == 0)
    check := CreateItemInContainer(mypack, objtype, 1);
    SetName(check, name);
  endif
  return check;
endfunction