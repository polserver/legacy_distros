use os;
use uo;
use util;

include "include/spellRestrictions";
include "include/attributes";

program stonewall( parms )
  var circle := 3;
  var delivery := "indirect";
  var noto := "neutral";
  var result := maincast(parms, delivery, circle, noto, 24);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  var fields := {};
  var duration;
  var listeners := {};
  var sufferers := {};
  var x := cast_on.x;
  var y := cast_on.y;
  var z := cast_on.z;
  case (get_direction(caster, x, y, z))
	"north" : fields:= CreateNorthSouthField(caster, 0x80, x, y, z, 5);
	"south" : fields:= CreateNorthSouthField(caster, 0x80, x, y, z, 5);
	"east"  : fields:= CreateEastWestField(caster, 0x80, x, y, z, 5);
	"west"  : fields:= CreateEastWestField(caster, 0x80, x, y, z, 5);
	default:
	  case (RandomInt(2))
		0: fields:= CreateNorthSouthField(caster, 0x80, x, y, z, 4);
        1: fields:= CreateEastWestField(caster, 0x80, x, y, z, 4);
	  endcase
  endcase
  Detach();
  duration := CInt(GetEffectiveSkill( caster, SKILLID_MAGERY ) )/2;
  if (duration < 20)
    duration := 20;
  endif
  var i;
  for (i := 1; i <= 5; i := i + 1)
    PlaySoundEffect(fields[i], SFX_SPELL_WALL_OF_STONE);
  endfor
  sleep(duration);
  foreach fieldpart in fields
    DestroyItem(fieldpart);
  endforeach
endprogram