use uo;
use os;
use npc;
use util;
use cfgfile;

include "include/dist";
include "include/client";
include "include/res";
include "include/resPenalty";
include "include/canAccess";
include "include/attributes";

var clock := ReadGameClock();

program use_bandages (who, item)
  EraseObjProperty(who, "IsMeditating");
  if(!can_access(who, item))
    return;
  endif
  if(item.movable = 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  var Sleeptime;
  if(!ReserveItem(item))
    return;
  endif
  SendSysmessage(who, "who would you like to heal?");
  var patient := Target(who, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS);
  if(!patient)
    SendSysmessage(who, "Cancelled.");
    return;
  endif
  var Anatomy:= GetBaseSkill(who, SKILLID_ANATOMY);
  var poison_level := Cint(GetObjProperty(patient, "poison_level"));
  if(dist(who, patient) >= 2)
    SendSysMessage(who, "Your patient is too far");
    return;
  elseif(!CheckLineOfSight(who,patient))
	SendSysMessage(who,"You cant see your patient");
	return;
  elseif((GetHp(patient) >= GetMaxHp(patient)) and (poison_level < 1))
	SendSysMessage(who,"Your patient is at full heath");
	return;
  endif
  detach();
  SetObjProperty(who, "HealTimer", clock);
  if((patient.graphic = 0x190) or (patient.graphic = 0x191) )
  	If(Patient.serial = who.serial)
	  if(poison_level > 0)
        SendSysMessage(who,"You start to cure yourself");
        if(SubtractAmount(Item , 1))
		  cure(who, patient);
        endif
      else
        SendSysMessage(who,"You start to heal yourself");
        if(SubtractAmount(Item , 1))
		  heal(who, patient);
        endif
	  endif
    elseif(poison_level > 0)
      if(Patient.dead)
		SendSysMessage(who,"Your patient is dead");
		return;
      elseif(!CheckLineOfSight(who,patient))
        SendSysMessage(who,"You cant see you patient");
        return;
      endif
      if(SubtractAmount( Item , 1 ) and (dist(who, patient) <= 1))
        SendSysMessage(who,"You start to cure " + patient.name);
        SendSysMessage(patient,who.name + " starts to cure you");
        cure(who, patient);
      else
        SendSysMessage(who, "Your Patient is too far");
        return;
      endif
	else
      if(Patient.dead)
		SendSysMessage(who,"Your patient is dead");
		return;
	  elseif(!CheckLineOfSight(who, patient))
		SendSysMessage(who,"You cant see you patient");
        return;
      endif
      if(SubtractAmount(Item , 1) and (dist(who, patient) <= 1))
        SendSysMessage(who,"You start to heal "+patient.name);
        SendSysMessage(patient,who.name+" starts to heal you");
        heal(who, patient);
      else
        SendSysMessage(who, "Your patient is too far");
        return;
      endif
    endif
  elseif(patient.dead)
    if(!CheckLineOfSight(who,patient))
	  SendSysMessage(who,"You cant see your patient anymore");
	  return;
    endif
    if(SubtractAmount( Item , 1 ) and (dist(who, patient) <= 1))
      SendSysMessage(who,"You start to resurrect " + patient.name);
      SendSysMessage(patient, who.name + " starts to resurrect you");
      resurrect_pc (who, patient);
    else
      SendSysMessage(who, "Your Patient is too far");
      return;
    endif
  elseif(patient.npctemplate)
    var parms := {};
    parms[1] := patient;
    parms[2] := who;
    if(dist(who, patient) < 2)
      if(SubtractAmount(Item, 1))
        start_script(":veterinary:vet", parms);
        return;
      endif
    else
      SendSysMessage(who, "Your patient is too far");
      return;
    endif
  else
    SendSysmessage( who , "You cant heal that." );
    return;
  endif
endprogram

function heal(who, patient)
  var chp;
  var healing := (Cint(GetBaseSkill(who, SKILLID_HEALING)) / 2);
  var anatomy := (Cint(GetBaseSkill(who, SKILLID_ANATOMY)) / 5);
  var healamt := healing + anatomy + 10;
  var basehealamt := healamt;
  var counter := 0;
  var cycles;
  var count := 0;
  if(patient = who)
    var dexcheck := (GetBaseDexterity(who) / 20);
    cycles := (15 - dexcheck);
  else
    if(GetBaseDexterity(who) <= 33)
      cycles := 5;
    elseif(GetBaseDexterity(who) <= 67)
      cycles := 4;
    else
      cycles := 3;
    endif
  endif
  var chk := 0;
  while(counter <= cycles)
    if(!GetObjProperty(who, "HealTimer"))
      chk := 1;
      break;
    elseif(clock != GetObjProperty(who, "HealTimer"))
      break;
    endif
    chp := GetHP(who);
    sleep(1);
    if(dist(who, patient) > 1)
      sendsysmessage(who,"Your patient isnt close enough.");
      sendsysmessage(patient, who.name + "has stopped healing you.");
      break;
    endif
    if(GetHP(who) < chp)
      if((RandomInt(basehealamt)+1) >= GetBaseDexterity(who))
        SendSysMessage( who, "Your fingers slip." );
        healamt := (healamt * 0.90);
        count := count + 1;
        if(count >= 5)
          healamt := 5;
        endif
      endif
    endif
    if(patient.graphic = 970)
      resurrect_pc(who, patient);
      break;
    endif
    if(getobjproperty(patient,"poison_level"))
      cure(who, patient);
      break;
    endif
    if(counter >= cycles)
      var diff := 100 - Cint((GetHP(patient) * 100.0) / GetMaxHP(patient));
      if(CheckSkill(who, SKILLID_HEALING, diff, 0))
        if(healamt <= 5)
          sendsysmessage(who,"You finish applying the bandages, but they barely help.");
        endif
        if((GetMaxHP(patient) - GetHP(patient)) >= healamt)
          healdamage(patient, healamt);
          AwardPoints(who, SKILLID_HEALING, (healamt * 2));
        else
          healamt := (GetMaxHP(patient) - GetHP(patient));
          healdamage(patient, healamt);
          AwardPoints(who, SKILLID_HEALING, (healamt * 2));
        endif
        if(GetHP(patient) >= GetMaxHP(patient))
		  sendsysmessage(who, "you have healed your patient completely");
		  sendsysmessage(patient, who.name + " has healed you completely");
        else
		  sendsysmessage(who, "you successfully heal " + patient.name);
		  sendsysmessage(patient, who.name + " has healed you.");
	    endif
        break;
      else
        SendSysmessage ( who , "You could not heal him." );
      endif
    endif
    counter := counter + 1;
  endwhile
  if(chk = 1)
    SendSysMessage(who, "You stop healing your patient.");
    return;
  endif
endfunction

function cure(who, patient)
  var chp;
  var healing := (Cint(GetBaseSkill(who, SKILLID_HEALING)) / 25);
  var anatomy := (Cint(GetBaseSkill(who, SKILLID_ANATOMY)) / 50);
  var healamt := healing + anatomy;
  var counter := 0;
  var cycles;
  var count := 0;
  if(patient = who)
    var dexcheck := (GetBaseDexterity(who) / 33);
    cycles := (15 - dexcheck);
  else
    if(GetBaseDexterity(who) <= 33)
      cycles := 5;
    elseif(GetBaseDexterity(who) <= 67)
      cycles := 4;
    else
      cycles := 3;
    endif
  endif
  var chk := 0;
  while(counter <= cycles)
    if(!GetObjProperty(who, "HealTimer"))
      chk := 1;
      break;
    elseif(clock != GetObjProperty(who, "HealTimer"))
      break;
    endif
    chp:=GetHP(who);
    sleep(1);
    if(dist(who, patient) > 1)
      sendsysmessage(who,"Your patient isnt close enough.");
      sendsysmessage(patient, who.name + "has stopped healing you.");
      break;
    endif
	if(GetHP(who) < chp)
      if((RandomInt(100)+1) > GetBaseDexterity(who))
	    SendSysMessage( who, "Your fingers slip." );
        healamt := (healamt - 1);
        count := count + 1;
        if(count >= 5)
          healamt := 0;
        endif
      endif
	endif
    if(patient.graphic = 970)
      resurrect_pc(who, patient);
      break;
    endif
    if(counter >= cycles)
      var poison_level := GetObjProperty(patient,"poison_level");
      if(CheckSkill(who, SKILLID_HEALING, 65, 0) and (Cint(GetBaseSkill(who, SKILLID_HEALING)) >= 60))
        if(healamt > 0)
          if(healamt >= poison_level)
            EraseObjProperty(patient,"poison_level");
            SendSysMessage(who,"You have cured your patient completely.");
            SendSysMessage(patient, who.name + "has cured you completely.");
            AwardPoints(who, SKILLID_HEALING, (healamt * 100));
          else
            SetObjProperty(patient,"poison_level", (poison_level - healamt));
            SendSysMessage(who,"You have partially cured your patient.");
            SendSysMessage(patient, who.name + "has partially cured you .");
            AwardPoints(who, SKILLID_HEALING, (healamt * 80));
          endif
        else
          sendsysmessage(who,"You finish applying the bandages, but they barely help.");
        endif
        break;
      else
        SendSysmessage ( who , "You could not heal him." );
      endif
    endif
    counter := counter + 1;
  endwhile
  if(chk = 1)
    SendSysMessage(who, "You stop healing your patient.");
    return;
  endif
endfunction

function resurrect_pc(who, patient)
  var chp;
  var healamt := Cint(GetBaseSkill(who, SKILLID_HEALING));
  var basehealamt := healamt;
  var counter := 0;
  var chk := 0;
  while(counter <= 10)
    if(!GetObjProperty(who, "HealTimer"))
      chk := 1;
      break;
    elseif(clock != GetObjProperty(who, "HealTimer"))
      break;
    endif
    chp:=GetHP(who);
    sleep(1);
    if(dist(who, patient) > 1)
      sendsysmessage(who,"your patient isnt close enough.");
      sendsysmessage(patient, who.name + "has stopped healing you.");
      break;
    endif
	if(GetHP(who) < chp)
      if((RandomInt(100)+1) > GetBaseDexterity(who))
	    SendSysMessage(who, "Your fingers slip.");
        healamt := (healamt * 0.9);
      endif
	endif
    if(getobjproperty(patient,"poison_level"))
      EraseObjProperty(patient,"poison_level");
    endif
    if(counter >= 10)
      if((CheckSkill(who, SKILLID_HEALING, 85, 0)) and (Cint(GetBaseSkill(who, SKILLID_ANATOMY)) >= 81) and (Cint(GetBaseSkill(who, SKILLID_HEALING)) >= 81))
        if((RandomInt(basehealamt) + 1) < healamt);
          AwardPoints(who, SKILLID_HEALING, 400);
          if(ResNow(patient)=1)
            resurrect(patient);
            ResPenalties(patient);
            SendSysmessage(who, "You have resurrected " + patient.name);
          else
            SendSysmessage(who, "The patient refused resurrection." );
          endif
        else
          SendSysmessage(who, "You could not resurrect him.");
        endif
      else
        SendSysmessage(who, "You could not resurrect him.");
        break;
      endif
    endif
    counter := counter + 1;
  endwhile
  if(chk = 1)
    SendSysMessage(who, "You stop healing your patient.");
    return;
  endif
endfunction
